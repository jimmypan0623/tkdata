****A32.報表格式明細設定作業
CLOSE ALL
CLEAR 
FLG='0'
FCH=''
CHK=''
R=0
AREA1='A02_1'
AREA2='A32'
IF !USED('A01')
   SELECT  0
   USE A01 INDE A01
ELSE
   SELECT  A01
ENDIF      
SET ORDER TO A01
IF !USED('A03')
   SELECT  0
   USE A03
ELSE 
   SELECT  A03
ENDIF
SET ORDER TO A03
IF !USED('A02')
   SELE 0
   USE A02 INDE A02
ELSE 
   SELE A02
ENDIF
SET ORDER TO A021  
SELECT A02
SEEK SYS_OPER+'A32' 
IF A02.F08 = '*'
   SELECT A03.F01 AS F03,A03.F02 AS F02 FROM A03 WHERE A03.F14 = .T. ORDER BY A03.F01 INTO CURSOR A02_1 READWRITE 
ELSE
   SELECT A02.F03,A03.F02 FROM A02,A03 WHERE ALLTRIM(A02.F01) = ALLTRIM(SYS_OPER) AND A02.F03 = A03.F01 ORDER BY A03.F01 INTO CURSOR A02_1 READWRITE 
ENDIF
SELECT A02_1
INDE ON F03 TAG A02_11
IF !USED('A32')
   SELE 0
   USE A32 INDE A32
ELSE 
   SELE A32
ENDIF
SET ORDER TO A321 
SELECT A02_1
SET ORDER TO A02_11
GO TOP
********* 
A32FORM=CREATEOBJECT("TKA32")
A32FORM.SHOW  
DEFINE CLASS TKA32 AS FORM
  CAPTION='A32.報表格式明細設定作業'
*!*	  AUTOCENTER=.T.
  CONTROLBOX=.F.
  BORDERSTYLE=1  
  MAXBUTTON=.F.
  MINBUTTON=.F.
  MOVABLE=.F.
  CLOSABLE=.F.
  CONTROLCOUNT=57
  FONTSIZE=INT_015*9
  HEIGHT=INT_015*550
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WIDTH=INT_015*789
  WINDOWTYPE=1
  NAME='TKA32'
  ADD OBJECT SHAPE1 AS SHAPE1 WITH;
      AUTOCENTER=.T.
  ADD OBJECT SHAPE2 AS SHAPE2 WITH;
      AUTOCENTER=.T.
  ADD OBJECT SHAPE3 AS SHAPE3 WITH;
      AUTOCENTER=.T.
  ADD OBJECT KEY_LIST AS KEY_LIST WITH;
      AUTOCENTER=.T.
  ADD OBJECT GRID1 AS GRID1 WITH;
      COLUMNCOUNT=2,;            
      RECORDSOURCE='A02_1',;
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2'      
  ADD OBJECT GRID2 AS GRID2 WITH;
      COLUMNCOUNT=5,;            
      RECORDSOURCE='A32',;
      LINKMASTER='A02_1',;
      RELATIONALEXPR='F03',;
      CHILDORDER='A321',; 
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2',;
      COLUMN3.NAME='COLUMN3',;
      COLUMN4.NAME='COLUMN4',;
      COLUMN5.NAME='COLUMN5'
  ADD OBJECT CMDGROUP AS CMDGROUP WITH;
      AUTOSIZE=.T.
  ADD OBJECT ORPGROUP AS ORPGROUP WITH;    
      AUTOSIZE=.T.
  ADD OBJECT SHRGROUP AS SHRGROUP WITH;
      TOP=INT_015*140,;
      LEFT=INT_015*600          
  ADD OBJECT REPORT_FORMAT AS COMMANDBUTTON WITH;
      TOP=INT_015*490,;
      LEFT=INT_015*13,;
      HEIGHT=INT_015*25,;
      FONTSIZE=INT_015*9,;
      WIDTH=INT_015*70,;      
      CAPTION='\<R.格式設定',;
      NAME='REPORT_FORMAT '     
 ************     
  ADD OBJECT LBLF011 AS LABEL WITH;
      LEFT=INT_015*244,;
      TOP=INT_015*29,;
      WIDTH=INT_015*50,;
      CAPTION='程式編號' 
  ADD OBJECT TXTF011 AS TEXTBOX WITH;      
      LEFT=INT_015*295,;
      TOP=INT_015*23,;
      WIDTH=INT_015*35,;
      HEIGHT=INT_015*25,;
      MAXLENGTH=3,;
      NAME='TXTF011'    
  ADD OBJECT LBLF022 AS LABEL WITH;      
      LEFT=INT_015*244,;
      TOP=INT_015*57,;
      WIDTH=INT_015*50,;
      CAPTION='程式名稱'
 ADD OBJECT TXTF022 AS TEXTBOX WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*51,;
      WIDTH=INT_015*136,;
       HEIGHT=INT_015*25,;
      MAXLENGTH=20,;
      NAME='TXTF022'   
  ADD OBJECT LBLF02 AS LABEL WITH;      
      LEFT=INT_015*244,;
      TOP=INT_015*85,;
      WIDTH=INT_015*50,;
      CAPTION='報表檔名'
 ADD OBJECT TXTF02 AS TEXTBOX WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*79,;
      WIDTH=INT_015*100,;
      HEIGHT=INT_015*25,;
      MAXLENGTH=10,;
      NAME='TXTF02'   
  ADD OBJECT LBLF03 AS LABEL WITH;      
      LEFT=INT_015*244,;
      TOP=INT_015*113,;
      WIDTH=INT_015*50,;
      CAPTION='報表格式'
 ADD OBJECT TXTF03 AS TEXTBOX WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*107,;
      WIDTH=INT_015*80,;
      HEIGHT=INT_015*25,;
      MAXLENGTH=10,;
      NAME='TXTF03'  
  ADD OBJECT COMBOBOX03 AS COMBOBOX WITH;    
      LEFT=INT_015*295,;
      TOP=INT_015*107,;       
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*80,;
      FONTSIZE=INT_015*9,;
      BOUNDCOLUMN=1,;
      ROWSOURCETYPE=1,;
      STYLE=2      
  ADD OBJECT LBLF04 AS LABEL WITH;      
      LEFT=INT_015*244,;
      TOP=INT_015*141,;
      WIDTH=INT_015*50,;
      CAPTION='列印方向'
 ADD OBJECT TXTF04 AS TEXTBOX WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*138,;
      WIDTH=INT_015*50,;
      HEIGHT=INT_015*25,;
      MAXLENGTH=10,;
      NAME='TXTF04'   
  ADD OBJECT COMBOBOX04 AS COMBOBOX WITH;    
      LEFT=INT_015*295,;
      TOP=INT_015*138,;       
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*50,;
      FONTSIZE=INT_015*9,;
      BOUNDCOLUMN=1,;
      ROWSOURCETYPE=1,;
      STYLE=2           
  ADD OBJECT LBLF05 AS LABEL WITH;      
      LEFT=INT_015*244,;
      TOP=INT_015*169,;
      WIDTH=INT_015*50,;
      CAPTION='報表抬頭'
 ADD OBJECT TXTF05 AS TEXTBOX WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*166,;
      WIDTH=INT_015*290,;
      HEIGHT=INT_015*25,;
      MAXLENGTH=60,;
      NAME='TXTF05'       
 ADD OBJECT LBLF99 AS LABEL WITH;      
      LEFT=INT_015*600,;
      TOP=INT_015*175,;
      WIDTH=INT_015*50,;
      CAPTION='安全資料'      
 ADD OBJECT LBLF991 AS LABEL WITH;
      LEFT=INT_015*650,;
      TOP=INT_015*175,;
      AUTOSIZE=.T.,;
      CAPTION='' 
  PROCEDURE COMBOBOX03.INIT
     WITH THIS 
          .VISIBLE=.F.
          .VALUE=1
          .ADDITEM('中一刀')
          .ADDITEM('標籤')
          .ADDITEM('A3')
          .ADDITEM('A4')
          .ADDITEM('A5') 
          .ADDITEM('B4') 
          .ADDITEM('B5') 
          .ADDITEM('Legal') 
          .ADDITEM('Ledger') 
          .ADDITEM('Letter') 
     ENDWITH   
  ENDPROC  
  PROCEDURE COMBOBOX04.INIT
     WITH THIS 
          .VISIBLE=.F.
          .VALUE=1
          .ADDITEM('直式')
          .ADDITEM('橫式')
     ENDWITH
  ENDPROC
**********
  PROC INIT   
       THISFORM.SETALL('HEIGHT',INT_015*25,'TEXTBOX')
       THISFORM.SETALL('HEIGHT',INT_015*17,'LABEL')
       THISFORM.SETALL('FONTSIZE',INT_015*9,'TEXTBOX')
       THISFORM.SETALL('FONTSIZE',INT_015*9,'LABEL')
       THISFORM.SETALL('READONLY',.T.,'TEXTBOX')
       THISFORM.SETALL('READONLY',.T.,'CHECKBOX')
       THISFORM.SETALL('AUTOSIZE',.T.,'CHECKBOX')
       THISFORM.SETALL('FONTSIZE',INT_015*9,'CHECKBOX')       
       THISFORM.SHRGROUP.VISIBLE=.F.
       THISFORM.SHRGROUP.ENABLED=.F.
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'HEADER') 
       THISFORM.GRID1.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)
       THISFORM.GRID2.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)      
       THISFORM.GRID1.COLUMN1.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN2.WIDTH=INT_015*130
       THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='程式編號'
       THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='程式名稱'
       THISFORM.GRID1.COLUMN1.CONTROLSOURCE='A02_1.F03'
       THISFORM.GRID1.COLUMN2.CONTROLSOURCE='A02_1.F02'
       THISFORM.GRID2.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID2.SETALL('FONTSIZE',INT_015*9,'HEADER')       
       THISFORM.GRID2.COLUMN1.HEADER1.CAPTION='報表檔名'
       THISFORM.GRID2.COLUMN2.HEADER1.CAPTION='報表格式'
	   THISFORM.GRID2.COLUMN3.HEADER1.CAPTION='列印方向'
	   THISFORM.GRID2.COLUMN4.HEADER1.CAPTION='報表抬頭'
	   THISFORM.GRID2.COLUMN5.HEADER1.CAPTION='安全資料' 
	   THISFORM.GRID2.COLUMN1.CONTROLSOURCE='A32.F02'
	   THISFORM.GRID2.COLUMN2.CONTROLSOURCE='A32.F03'
	   THISFORM.GRID2.COLUMN3.CONTROLSOURCE="ICASE(A32.F04 = 1,'直式',A32.F04 = 2,'橫式','')"
	   THISFORM.GRID2.COLUMN4.CONTROLSOURCE='A32.F05'
	   THISFORM.GRID2.COLUMN5.CONTROLSOURCE='A32.F99'
	   THISFORM.GRID2.COLUMN1.WIDTH=INT_015*80
	   THISFORM.GRID2.COLUMN2.WIDTH=INT_015*70
	   THISFORM.GRID2.COLUMN3.WIDTH=INT_015*55
	   THISFORM.GRID2.COLUMN4.WIDTH=INT_015*240
	   THISFORM.GRID2.COLUMN5.WIDTH=INT_015*110
       THISFORM.GRID1.READONLY=.T.       
       THISFORM.GRID2.READONLY=.T.           
       THISFORM.GRID1.SETFOCUS     
       IF THISFORM.GRID1.ACTIVEROW=0
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
          THISFORM.ORPGROUP.PNT_BOTT.ENABLED=.F.          
       ELSE
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T. 
          THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限                
       ENDIF        
       THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=.F.	&&判斷有無修改的權限
       THISFORM.ORPGROUP.DEL_BOTT.ENABLED=.F.   &&判斷有無刪除的權限    
       THISFORM.ORPGROUP.NEW_BOTT.ENABLED=IIF(A02.F04='*',.T.,.F.)  &&判斷有無新增的權限  
       THISFORM.KEY_LIST.DISPLAYVALUE='依程式編號排列'       
       JK='程式編號'
       THISFORM.TXTF011.VALUE=A02_1.F03
       THISFORM.TXTF022.VALUE=A02_1.F02
       THISFORM.TXTF02.VALUE=A32.F02
       THISFORM.TXTF03.VALUE=A32.F03
       THISFORM.TXTF04.VALUE=ICASE(A32.F04 = 1,'直式',A32.F04 = 2,'橫式','')
       THISFORM.TXTF05.VALUE=A32.F05
       THISFORM.COMBOBOX03.DISPLAYVALUE=A32.F03
       THISFORM.COMBOBOX04.VALUE=A32.F04       
       THISFORM.LBLF991.CAPTION=A32.F99
  ENDPROC        
***********   
  PROC KEY_LIST.INIT 
       WITH THIS 
           .ADDITEM('依程式編號排列')
       ENDWITH      
  ENDPROC 
  **********
  PROC GRID1.AFTERROWCOLCHANGE
       LPARAMETERS NCOLINDEX
       THISFORM.REPORT_FORMAT.ENABLED=.F. 
       THISFORM.GRID1.SETALL("DYNAMICBACKCOLOR","","COLUMN")
       IF THIS.ACTIVEROW=0
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.        
       ELSE
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.
	      THISFORM.TXTF011.VALUE=A02_1.F03
	      THISFORM.TXTF022.VALUE=A02_1.F02
	      THISFORM.TXTF02.VALUE=''
	      THISFORM.TXTF03.VALUE=''
	      THISFORM.TXTF04.VALUE=''
	      THISFORM.TXTF05.VALUE=''
	      THISFORM.LBLF991.CAPTION=''
       ENDIF   
       THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=.F.
       THISFORM.ORPGROUP.DEL_BOTT.ENABLED=.F.        
       FCH=THISFORM.ACTIVECONTROL.NAME
       JK=THIS.COLUMN1.HEADER1.CAPTION         
       CHK=''     
       THISFORM.REFRESH
  ENDPROC     
  ***********
  PROC GRID2.AFTERROWCOLCHANGE
       LPARAMETERS NCOLINDEX  
       R=RECNO('&AREA1')
       THISFORM.GRID1.SETALL("DYNAMICBACKCOLOR","IIF(RECNO('&AREA1')=R,RGB(255,255,0),'')","COLUMN")             
       THISFORM.GRID1.ENABLED=.F.
       THISFORM.TXTF02.VALUE=A32.F02
       THISFORM.TXTF03.VALUE=A32.F03
       THISFORM.TXTF04.VALUE=ICASE(A32.F04 = 1,'直式',A32.F04 = 2,'橫式','')
       THISFORM.TXTF05.VALUE=A32.F05
       THISFORM.COMBOBOX03.DISPLAYVALUE=A32.F03
       THISFORM.COMBOBOX04.VALUE=A32.F04
       THISFORM.LBLF991.CAPTION=A32.F99
       IF THIS.ACTIVEROW=0
          THISFORM.REPORT_FORMAT.ENABLED=.F. 
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
          THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=.F.
          THISFORM.ORPGROUP.DEL_BOTT.ENABLED=.F.       
       ELSE
          THISFORM.REPORT_FORMAT.ENABLED=.T. 
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.
          THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=IIF(A02.F05='*',.T.,.F.)  &&判斷有無修改的權限
          THISFORM.ORPGROUP.DEL_BOTT.ENABLED=IIF(A02.F06='*',.T.,.F.)   &&判斷有無刪除的權限
       ENDIF
       FCH=THISFORM.ACTIVECONTROL.NAME
       JK=THIS.COLUMN1.HEADER1.CAPTION
       CHK=THISFORM.TXTF011.VALUE 
  ENDPROC 
*****  
  PROC GRID1.MOUSEENTER
       LPARAMETERS nButton, nShift, nXCoord, nYCoord
         IF THISFORM.SHRGROUP.VISIBLE=.F. 
             THISFORM.GRID1.ENABLED=.T. 
         ELSE    
             THISFORM.GRID1.ENABLED=.F.                         
         ENDIF    
 ENDPROC    
  ***********
  PROCEDURE ORPGROUP.NEW_BOTT.CLICK    
     THISFORM.REPORT_FORMAT.ENABLED=.F.
     THISFORM.GRID1.ENABLED=.F. 
     THISFORM.GRID2.ENABLED=.F. 
     THISFORM.SETALL('READONLY',.F.,'TEXTBOX')   
     THISFORM.TXTF011.READONLY=.T. 
     THISFORM.TXTF022.READONLY=.T. 
     THISFORM.TXTF02.VALUE=''  
     THISFORM.TXTF03.VALUE='' 
     THISFORM.TXTF04.VALUE=''       
     THISFORM.TXTF05.VALUE=''
     THISFORM.LBLF991.CAPTION=''  
     THISFORM.TXTF03.VISIBLE = .F.
     THISFORM.COMBOBOX03.VISIBLE = .T.
     THISFORM.COMBOBOX03.VALUE = 1
     THISFORM.TXTF04.VISIBLE = .F.
     THISFORM.COMBOBOX04.VISIBLE = .T.
     THISFORM.COMBOBOX04.VALUE = 1     
     THISFORM.TXTF02.SETFOCUS                
  ENDPROC 
******   
  PROCEDURE SHRGROUP.SHURE1_BOTT.CLICK    
    IF EMPTY(THISFORM.TXTF02.VALUE) = .T.
       =MESSAGEBOX('報表檔名不可空白!',0+48,'提示訊息視窗')
       THISFORM.TXTF02.SETFOCUS
       RETURN     
    ENDIF
    IF INDEXSEEK(THISFORM.TXTF011.VALUE + THISFORM.TXTF02.VALUE,.F.,'A32','A321') = .T.
       =MESSAGEBOX('此報表檔名已存在,請重新輸入!',0+48,'提示訊息視窗')
       THISFORM.TXTF02.SETFOCUS
       RETURN     
    ENDIF     
    IF EMPTY(THISFORM.COMBOBOX03.DISPLAYVALUE) = .T.
       =MESSAGEBOX('報表格式不可空白!',0+48,'提示訊息視窗')
       THISFORM.COMBOBOX03.SETFOCUS
       RETURN     
    ENDIF    
    IF EMPTY(THISFORM.COMBOBOX04.DISPLAYVALUE) = .T.
       =MESSAGEBOX('列印方向不可空白!',0+48,'提示訊息視窗')
       THISFORM.COMBOBOX04.SETFOCUS
       RETURN     
    ENDIF       
    IF EMPTY(THISFORM.TXTF05.VALUE) = .T.
       =MESSAGEBOX('報表抬頭不可空白!',0+48,'提示訊息視窗')
       THISFORM.TXTF05.SETFOCUS
       RETURN     
    ENDIF     
    SELECT A32
    APPEND BLANK
    LOCK()
    IF RLOCK() 
       REPLACE F01 WITH THISFORM.TXTF011.VALUE
       REPLACE F02 WITH THISFORM.TXTF02.VALUE
       REPLACE F03 WITH ALLTRIM(THISFORM.COMBOBOX03.DISPLAYVALUE)
       REPLACE F04 WITH THISFORM.COMBOBOX04.VALUE
       REPLACE F05 WITH THISFORM.TXTF05.VALUE
       REPLACE F99 WITH DTCX(DATE())+IIF(SEEK(SYS_OPER,'A01'),A01.F03,'')
    ENDIF
    UNLOCK  
    THISFORM.GRID2.ENABLED=.T.    
    THISFORM.GRID2.SETFOCUS    
    THISFORM.ORPGROUP.NEW_BOTT.CLICK            
  ENDPROC 
  *********
  PROCEDURE ORPGROUP.EDIT_BOTT.CLICK
     THISFORM.REPORT_FORMAT.ENABLED=.F.
     THISFORM.GRID1.ENABLED=.F. 
     THISFORM.GRID2.ENABLED=.F. 
     THISFORM.SETALL('READONLY',.F.,'TEXTBOX')    
     THISFORM.TXTF011.READONLY=.T. 
     THISFORM.TXTF022.READONLY=.T. 
     THISFORM.TXTF03.VISIBLE = .F.
     THISFORM.COMBOBOX03.VISIBLE = .T. 
     THISFORM.TXTF04.VISIBLE = .F.
     THISFORM.COMBOBOX04.VISIBLE = .T.          
	 THISFORM.TXTF02.SETFOCUS 
	 SELECT A32   
     LOCK()
     IF !RLOCK()
        DO FILE_IMPACT
        RETURN
     ENDIF
  ENDPROC  
  *********
  PROCEDURE SHRGROUP.SHURE2_BOTT.CLICK
    IF EMPTY(THISFORM.TXTF02.VALUE) = .T.
       =MESSAGEBOX('報表檔名不可空白!',0+48,'提示訊息視窗')
       THISFORM.TXTF02.SETFOCUS
       RETURN     
    ENDIF
    IF ALLTRIM(THISFORM.TXTF02.VALUE) <> ALLTRIM(A32.F02)
       IF INDEXSEEK(THISFORM.TXTF011.VALUE + THISFORM.TXTF02.VALUE,.F.,'A32','A321') = .T.
          =MESSAGEBOX('此報表檔名已存在,請重新輸入!',0+48,'提示訊息視窗')
          THISFORM.TXTF02.SETFOCUS
          RETURN     
       ENDIF   
    ENDIF     
    IF EMPTY(THISFORM.COMBOBOX03.DISPLAYVALUE) = .T.
       =MESSAGEBOX('報表格式不可空白!',0+48,'提示訊息視窗')
       THISFORM.COMBOBOX03.SETFOCUS
       RETURN     
    ENDIF    
    IF EMPTY(THISFORM.COMBOBOX04.DISPLAYVALUE) = .T.
       =MESSAGEBOX('列印方向不可空白!',0+48,'提示訊息視窗')
       THISFORM.COMBOBOX04.SETFOCUS
       RETURN     
    ENDIF     
    IF EMPTY(THISFORM.TXTF05.VALUE) = .T.
       =MESSAGEBOX('報表抬頭不可空白!',0+48,'提示訊息視窗')
       THISFORM.TXTF05.SETFOCUS
       RETURN     
    ENDIF  
    SELECT A32
    LOCK()
    IF RLOCK()
       REPLACE F02 WITH THISFORM.TXTF02.VALUE
       REPLACE F03 WITH ALLTRIM(THISFORM.COMBOBOX03.DISPLAYVALUE)
       REPLACE F04 WITH THISFORM.COMBOBOX04.VALUE
       REPLACE F05 WITH THISFORM.TXTF05.VALUE
       REPLACE F99 WITH DTCX(DATE())+IIF(SEEK(SYS_OPER,'A01'),A01.F03,'')    
    ELSE
       DO FILE_IMPACT
       RETURN
    ENDIF    
    THISFORM.SHRGROUP.ABANDON_BOTT.CLICK    
  ENDPROC   
****      
  PROCEDURE ORPGROUP.DEL_BOTT.CLICK
     IF MESSAGEBOX('是否確定要刪除此筆資料',4+32+256,'請確認') = 6	      
        SELECT A32   
        LOCK()
        IF RLOCK()
           DELETE 
           SKIP -1
           IF BOF()
              GO TOP
           ENDIF   
        ELSE
           DO FILE_IMPACT    
        ENDIF 
        UNLOCK        
     ENDIF   
     THISFORM.SHRGROUP.ABANDON_BOTT.CLICK  
  ENDPROC  
  **********  
  PROCEDURE SHRGROUP.ABANDON_BOTT.CLICK
      THISFORM.SHRGROUP.ENABLED=.F.
      THISFORM.SHRGROUP.VISIBLE=.F.      
      THISFORM.ORPGROUP.ENABLED=.T.   
      THISFORM.ORPGROUP.SETALL('FORECOLOR',RGB(0,0,0),'COMMANDBUTTON')
      THISFORM.CMDGROUP.ENABLED=.T.
      THISFORM.CMDGROUP.SETALL('ENABLED',.T.,'COMMANDBUTTON')
      THISFORM.CMDGROUP.SETALL('FORECOLOR',RGB(0,0,0),'COMMANDBUTTON')
      THISFORM.ORPGROUP.NEW_BOTT.ENABLED=IIF(A02.F04='*',.T.,.F.)  &&判斷有無新增的權限      
      THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)  &&判斷有無列印的權限              
      THISFORM.SETALL('READONLY',.T.,'TEXTBOX')  
      THISFORM.TXTF03.VISIBLE = .T.
      THISFORM.TXTF04.VISIBLE = .T.
      THISFORM.COMBOBOX03.VISIBLE = .F.   
      THISFORM.COMBOBOX04.VISIBLE = .F.   
      THISFORM.GRID2.ENABLED=.T.    
      THISFORM.GRID2.SETFOCUS  
      IF THISFORM.GRID2.ACTIVEROW=0 
         SELECT  A02_1
         THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=.F. &&判斷有無修改的權限
         THISFORM.ORPGROUP.DEL_BOTT.ENABLED=.F.  &&判斷有無刪除的權限
         THISFORM.REPORT_FORMAT.ENABLED=.F. 
         THISFORM.GRID1.ENABLED=.T.
         THISFORM.GRID1.SETFOCUS
         THISFORM.GRID1.REFRESH
      ELSE
         SELECT  A32
         THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=IIF(A02.F05='*',.T.,.F.)  &&判斷有無修改的權限
         THISFORM.ORPGROUP.DEL_BOTT.ENABLED=IIF(A02.F06='*',.T.,.F.)  &&判斷有無刪除的權限 
         THISFORM.REPORT_FORMAT.ENABLED=.T.         
         THISFORM.GRID2.SETFOCUS
         THISFORM.GRID2.REFRESH
      ENDIF
      UNLOCK ALL
  ENDPROC
  ***
  PROCEDURE SHRGROUP.FINISH_BOTT.CLICK
      THISFORM.SHRGROUP.ABANDON_BOTT.CLICK
  ENDPROC     
 **************格式設定
 PROC REPORT_FORMAT.CLICK
	THISFORM.GRID1.ENABLED=.F.
	THISFORM.GRID2.ENABLED=.F.
	ReportFileName = ALLTRIM(A32.f02) + '.frx'
	IF FILE(ALLTRIM(INT_116) + ReportFileName) = .T.
	   TRY
		  SELECT 0
		  USE ALLTRIM(INT_116) + ReportFileName EXCLUSIVE 
		  FORMAT_NO = SYS(1037,1) 
	  	  = FORMAT_NO 
		  USE 
		  IF FORMAT_NO = '1'
			 =MESSAGEBOX('完成報表格式之設定!',0+64,'提示訊息視窗')
		  ENDIF  
		CATCH
		  SELECT A32
		  =MESSAGEBOX('此報表已被開啟,請關閉使用中之報表後再做格式設定!',0+64,'提示訊息視窗')	
		ENDTRY		
	ELSE
	   SELECT A32
	   =MESSAGEBOX('找不到此報表檔,請查詢後再做格式設定!',0+64,'提示訊息視窗')
	ENDIF	 
    THISFORM.GRID2.ENABLED=.T.
    THISFORM.GRID2.SETFOCUS
*!*	    THISFORM.REFRESH
  ENDPROC
ENDDEFINE       
***********************************************列印程序
PROCEDURE PNT_PRC  
  
ENDPROC     
***** 
