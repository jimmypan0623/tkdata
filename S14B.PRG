**程式名稱:S14B.出勤年度查詢
SET EXCL OFF
CLOSE ALL
CLEAR 
AREA1='S14B_TEMP'
FLG='0'
FCH=''
CHK=''
YEAR_DATE=''
IF !USED('A01')
   SELE 0
   USE A01
ELSE 
   SELE A01
ENDIF
SET ORDER TO A01      
IF !USED('A02')
   SELE 0
   USE A02 INDE A02
ELSE
   SELE A02
ENDIF
SET ORDER TO A021      
SEEK SYS_OPER+'S14'    
IF !USED('S14')
   SELE 0
   USE S14 
ELSE
   SELE S14
ENDIF
SET ORDER TO S141
*********
S14BFORM=CREATEOBJECT("TKS14B")
S14BFORM.SHOW  
DEFINE CLASS TKS14B AS FORM
*!*	  AUTOCENTER=.T.
  CAPTION='S14B.出勤年度查詢  (請輸入西元年  例如:2004)'
  TOP=INT_015*200
  LEFT=INT_015*235    
  FONTSIZE=INT_015*9
  HEIGHT=INT_015*100
  WIDTH=INT_015*275
  MAXBUTTON=.F.
  MINBUTTON=.F.
  MOVABLE=.F.   
  CONTROLBOX=.F.
  BORDERSTYLE=1
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WINDOWTYPE=1
  NAME='TKS14B'
  ADD OBJECT LBLF01 AS LABEL WITH;
      LEFT=INT_015*20,;
      TOP=INT_015*46,;
      AUTOSIZE=.T.,;
      CAPTION='請輸入查詢之西元年份'
  ADD OBJECT TXTF01 AS TEXTBOX WITH;
      LEFT=INT_015*155,;
      TOP=INT_015*40,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*40,;
      MAXLENGTH=4,;
      INPUTMASK='9999',;   
      NAME='TXTF01'      
  ADD OBJECT CMND1 AS COMMANDBUTTON WITH;
      LEFT=INT_015*210,;
      TOP=INT_015*20,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*50,;
      FONTSIZE=INT_015*10,;
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<Y.確認',;
      TOOLTIPTEXT='確認所輸入之內容->ALT+Y',;
      NAME='CMND1'
 ADD OBJECT CMND2 AS COMMANDBUTTON WITH;
      LEFT=INT_015*210,;
      TOP=INT_015*55,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*50,;
      FONTSIZE=INT_015*10,;
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<C.取消',;
      TOOLTIPTEXT='取消此畫面!快速鍵->ALT+C'
      NAME='CMND2'
********************
 PROCEDURE INIT 
         THISFORM.SETALL('FONTSIZE',INT_015*10,'LABEL')
         THISFORM.SETALL('FONTSIZE',INT_015*10,'TEXTBOX')
         THISFORM.SETALL('FONTSIZE',INT_015*10,'COMMANDBUTTON')
         THISFORM.TXTF01.VALUE = ALLTRIM(STR(YEAR(DATE())))
         THISFORM.TXTF01.SETFOCUS
 ENDPROC      
********    
 PROC CMND1.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND1.TOP=INT_015*21
             THISFORM.CMND1.LEFT=INT_015*211
  ENDPROC     
  PROC CMND1.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND1.TOP=INT_015*20
             THISFORM.CMND1.LEFT=INT_015*210           
  ENDPROC 
 PROC CMND2.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND2.TOP=INT_015*56
             THISFORM.CMND2.LEFT=INT_015*211
  ENDPROC     
  PROC CMND2.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND2.TOP=INT_015*55
             THISFORM.CMND2.LEFT=INT_015*210                
  ENDPROC  
********確認鍵******
PROCEDURE CMND1.CLICK   
       IF EMPTY(THISFORM.TXTF01.VALUE)  
           =MESSAGEBOX('年份不得為空白',0+64,'提示視窗')
           THISFORM.TXTF01.SETFOCUS
           RETURN      
       ENDIF 	       
       IF LEN(ALLTRIM(THISFORM.TXTF01.VALUE))<4 
           =MESSAGEBOX('年份長度不能小於4碼',0+64,'提示視窗')
           THISFORM.TXTF01.SETFOCUS
           RETURN      
       ENDIF      
       YEAR_DATE=THISFORM.TXTF01.VALUE
       CREATE CURSOR S14B_TEMP;
        (F01 C(4),F02 C(8),F03 C(4),F04 C(8),F05 N(8),F06 N(8),F09 N(9,1),F10 N(9,1))
        INDE ON F01 TAG S14B_TEMP1
        INDE ON F03 TAG S14B_TEMP2   
	SELECT S14B_TEMP
	SET ORDER TO S14B_TEMP1
        SELECT * FROM S14 WHERE  IIF(A02.F11<>'*',ALLTRIM(S14.F01)=IIF(SEEK(sys_oper,'A01'),ALLTRIM(A01.F12),''),.T.)  AND LEFT(DTOS(F08),4)=ALLTRIM(THISFORM.TXTF01.VALUE) ORDER BY F01 INTO CURSOR S14_TEMP
	SELECT S14_TEMP
	GO TOP
	IF  _TALLY>0       
	     DO WHILE !EOF()
	            SELECT S14B_TEMP
	            SEEK S14_TEMP.F01
	            IF FOUND()
	                REPLACE S14B_TEMP.F05 WITH S14B_TEMP.F05+S14_TEMP.F05
	                REPLACE S14B_TEMP.F06 WITH S14B_TEMP.F06+S14_TEMP.F06
	                REPLACE S14B_TEMP.F09 WITH S14B_TEMP.F09+S14_TEMP.F09
	                REPLACE S14B_TEMP.F10 WITH S14B_TEMP.F10+S14_TEMP.F10    
	            ELSE
	                SELECT S14B_TEMP
	                APPEND BLANK
	                REPLACE S14B_TEMP.F01 WITH S14_TEMP.F01
	                REPLACE S14B_TEMP.F02 WITH S14_TEMP.F02
	                REPLACE S14B_TEMP.F03 WITH S14_TEMP.F03
	                REPLACE S14B_TEMP.F04 WITH S14_TEMP.F04
	                REPLACE S14B_TEMP.F05 WITH S14B_TEMP.F05+S14_TEMP.F05
	                REPLACE S14B_TEMP.F06 WITH S14B_TEMP.F06+S14_TEMP.F06
	                REPLACE S14B_TEMP.F09 WITH S14B_TEMP.F09+S14_TEMP.F09
	                REPLACE S14B_TEMP.F10 WITH S14B_TEMP.F10+S14_TEMP.F10                
	            ENDIF
	            SELECT S14_TEMP
	            SKIP
	     ENDDO
	     SELECT S14_TEMP
	     USE
	     SELECT S14B_TEMP
	     SET ORDER TO  S14B_TEMP1
	     GO TOP
	     TKS14B_SEEK=CREATEOBJECT("TKS14B_SEEK")  
	     TKS14B_SEEK.SHOW		 
	     CLOSE TABLE ALL
 	     THISFORM.RELEASE 	
       ELSE
	     =MESSAGEBOX('此年度範圍並無資料',0+64,'提示視窗')
	     THISFORM.TXTF01.SETFOCUS
       ENDIF                       
ENDPROC       
********離開鍵***********
 PROCEDURE CMND2.CLICK     
     CLOSE TABLE ALL
     THISFORM.RELEASE 
*!*	     RETURN  
     DO S14.PRG
 ENDPROC      
ENDDEFINE      
************************
DEFINE CLASS TKS14B_SEEK AS FORM      
*!*	  AUTOCENTER=.T.  
  CAPTION='S14B.出勤年度查詢'+SPACE(25)+'查詢年度：'+YEAR_DATE+' 年'
  HEIGHT=INT_015*550
  WIDTH=INT_015*791
  FONTSIZE=INT_015*9
  CONTROLBOX=.F.
  MAXBUTTON=.F.
  MINBUTTON=.F.  
  MOVABLE=.F.
  CLOSABLE=.F.    
  BORDERSTYLE=2
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WINDOWTYPE=1
  NAME='TKS14B_SEEK'         
  ADD OBJECT KEY_LIST AS KEY_LIST WITH;
     WIDTH=INT_015*180,;
      AUTOCENTER=.T.
  ADD OBJECT GRID1 AS GRID1 WITH;
      COLUMNCOUNT=8,;            
      RECORDSOURCE='S14B_TEMP',; 
      HEIGHT=INT_015*500,;     
      WIDTH=INT_015*780,;      
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2',;
      COLUMN3.NAME='COLUMN3',; 
      COLUMN4.NAME='COLUMN4',; 
      COLUMN5.NAME='COLUMN5',;             
      COLUMN6.NAME='COLUMN6',;       
      COLUMN7.NAME='COLUMN7',;
      COLUMN8.NAME='COLUMN8'
   ADD OBJECT CMDGROUP AS CMDGROUP WITH;
      TOP=INT_015*0,;
      LEFT=INT_015*205,;
      ENABLED=.T.,;
      VISIBLE=.T.            
  ADD OBJECT ORPGROUP AS ORPGROUP WITH;      
      TOP=INT_015*0,;
      LEFT=INT_015*405                                                  
  PROCEDURE INIT
       THISFORM.ORPGROUP.NEW_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.EDIT_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.DEL_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限    
       THISFORM.GRID1.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)   
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'HEADER')   
       THISFORM.GRID1.COLUMN1.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN2.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN3.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN4.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN5.WIDTH=INT_015*105
       THISFORM.GRID1.COLUMN6.WIDTH=INT_015*105
       THISFORM.GRID1.COLUMN7.WIDTH=INT_015*130
       THISFORM.GRID1.COLUMN8.WIDTH=INT_015*130
       THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='人員編號'
       THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='人員姓名'
       THISFORM.GRID1.COLUMN3.HEADER1.CAPTION='部門編號'	   
       THISFORM.GRID1.COLUMN4.HEADER1.CAPTION='部門名稱'	
       THISFORM.GRID1.COLUMN5.HEADER1.CAPTION='年度遲到總分鐘數'	   
       THISFORM.GRID1.COLUMN6.HEADER1.CAPTION='年度早退總分鐘數'
       THISFORM.GRID1.COLUMN7.HEADER1.CAPTION='年度平日加班總小時數'   
       THISFORM.GRID1.COLUMN8.HEADER1.CAPTION='年度假日加班總小時數'  
       THISFORM.GRID1.COLUMN1.CONTROLSOURCE='S14B_TEMP.F01'
       THISFORM.GRID1.COLUMN2.CONTROLSOURCE='S14B_TEMP.F02'
       THISFORM.GRID1.COLUMN3.CONTROLSOURCE='S14B_TEMP.F03'
       THISFORM.GRID1.COLUMN4.CONTROLSOURCE='S14B_TEMP.F04'
       THISFORM.GRID1.COLUMN5.CONTROLSOURCE='S14B_TEMP.F05'
       THISFORM.GRID1.COLUMN6.CONTROLSOURCE='S14B_TEMP.F06'
       THISFORM.GRID1.COLUMN7.CONTROLSOURCE='S14B_TEMP.F09'
       THISFORM.GRID1.COLUMN8.CONTROLSOURCE='S14B_TEMP.F10'
       THISFORM.GRID1.READONLY=.T.       
       THISFORM.GRID1.SETFOCUS    
        IF  THISFORM.GRID1.ACTIVEROW=0 
              THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.F.      
	      THISFORM.CMDGROUP.UP_BOTT.ENABLED=.F.
	      THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.F.
	      THISFORM.CMDGROUP.END_BOTT.ENABLED=.F.    
	      THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.	        
	      THISFORM.ORPGROUP.PNT_BOTT.ENABLED=.F.   
        ELSE
              THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.T.      
	      THISFORM.CMDGROUP.UP_BOTT.ENABLED=.T.
	      THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.T.
	      THISFORM.CMDGROUP.END_BOTT.ENABLED=.T.    
	      THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.	        
	      THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限       
        ENDIF          
       JK='人員編號'
  ENDPROC    
  PROC KEY_LIST.INIT 
       WITH THIS 
           .ADDITEM('依人員編號排列')
           .ADDITEM('依部門編號排列')      
       ENDWITH      
  ENDPROC 
  PROC KEY_LIST.INTERACTIVECHANGE                 
       SELE S14B_TEMP
       DO CASE
          CASE THIS.DISPLAYVALUE='依人員編號排列'    
                     THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='人員編號'
		     THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='人員姓名'
		     THISFORM.GRID1.COLUMN3.HEADER1.CAPTION='部門編號'	   
		     THISFORM.GRID1.COLUMN4.HEADER1.CAPTION='部門名稱'	   	
		     THISFORM.GRID1.COLUMN1.CONTROLSOURCE='S14B_TEMP.F01'
		     THISFORM.GRID1.COLUMN2.CONTROLSOURCE='S14B_TEMP.F02'
		     THISFORM.GRID1.COLUMN3.CONTROLSOURCE='S14B_TEMP.F03'
		     THISFORM.GRID1.COLUMN4.CONTROLSOURCE='S14B_TEMP.F04'		        
                     SET ORDER TO S14B_TEMP1
          CASE THIS.DISPLAYVALUE='依部門編號排列'         
		     THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='部門編號'	   
		     THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='部門名稱'	   	
                     THISFORM.GRID1.COLUMN3.HEADER1.CAPTION='人員編號'
		     THISFORM.GRID1.COLUMN4.HEADER1.CAPTION='人員姓名'		     
		     THISFORM.GRID1.COLUMN1.CONTROLSOURCE='S14B_TEMP.F03'
		     THISFORM.GRID1.COLUMN2.CONTROLSOURCE='S14B_TEMP.F04'	
		     THISFORM.GRID1.COLUMN3.CONTROLSOURCE='S14B_TEMP.F01'
		     THISFORM.GRID1.COLUMN4.CONTROLSOURCE='S14B_TEMP.F02'		     	        
                     SET ORDER TO S14B_TEMP2       
       ENDCASE 
       JK=THISFORM.GRID1.COLUMN1.HEADER1.CAPTION
       THISFORM.GRID1.REFRESH
       THISFORM.GRID1.SETFOCUS
  ENDPROC             
*******        
  PROC GRID1.AFTERROWCOLCHANGE
       LPARAMETERS NCOLINDEX
       FCH=THISFORM.ACTIVECONTROL.NAME
       CHK=''
       THISFORM.REFRESH
  ENDPROC       
ENDDEFINE        
***********************************************列印程序
PROCEDURE PNT_PRC  
   SELE S14B_TEMP
   ANS_RANGE=CREATEOBJECT("ANS_RANGE")  
   ANS_RANGE.SHOW       
ENDPROC 
***********************************************列印範圍選定
DEFINE CLASS ANS_RANGE AS FORM
*!*	  AUTOCENTER=.T.
  CAPTION='請輸入列印資料範圍'
  TOP=INT_015*200
  LEFT=INT_015*250   
  HEIGHT=INT_015*120
  WIDTH=INT_015*280
  FONTSIZE=INT_015*9  
  MAXBUTTON=.F.
  MINBUTTON=.F.  
  MOVABLE=.F.
  CONTROLBOX=.F.
  BORDERSTYLE=2
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WINDOWTYPE=1
  NAME='ANS_RANGE'
  ADD OBJECT LABEL1 AS LABEL WITH;
      AUTOSIZE=.T.,;
      HEIGHT=INT_015*25,;
      LEFT=INT_015*30,;
      TOP=INT_015*30,;
      CAPTION='起始員工編號',;
      NAME='LABEL1'
  ADD OBJECT TEXT1 AS TEXTBOX WITH;
      TOP=INT_015*24,;
      LEFT=INT_015*110,;
      HEIGHT=INT_015*25,; 
      WIDTH=INT_015*60,; 
      MAXLENGTH=4,;
      NAME='TEXT1'      
  ADD OBJECT LABEL2 AS LABEL WITH;
      AUTOSIZE=.T.,;
      HEIGHT=INT_015*25,;
      LEFT=INT_015*30,;
      TOP=INT_015*61,;
      CAPTION='截止員工編號',;
      NAME='LABEL2'
  ADD OBJECT TEXT2 AS TEXTBOX WITH;
      TOP=INT_015*55,;
      LEFT=INT_015*110,;
      HEIGHT=INT_015*25,;   
      WIDTH=INT_015*60,;     
      MAXLENGTH=4,;   
      NAME='TEXT2'    
  ADD OBJECT CMND1 AS COMMANDBUTTON WITH;
      LEFT=INT_015*190,;    
      HEIGHT=INT_015*25,; 
      WIDTH=INT_015*70,;    
      TOP=INT_015*14,;  
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<P.列         印',;
      TOOLTIPTEXT='列印所輸入的範圍鍵值!快速鍵->ALT+Y',;
      NAME='CMND1'
  ADD OBJECT CMND2 AS COMMANDBUTTON WITH;
      LEFT=INT_015*190,;    
      HEIGHT=INT_015*25,; 
      WIDTH=INT_015*70,;    
      TOP=INT_015*45,;  
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<E.轉EXCEL',;
      TOOLTIPTEXT='將資料轉至EXCEL格式!快速鍵->ALT+Y',;
      NAME='CMND2'      
  ADD OBJECT CMND3 AS COMMANDBUTTON WITH;
      LEFT=INT_015*190,;
      TOP=INT_015*76,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*70,; 
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<C.取        消',;
      TOOLTIPTEXT='取消作業!快速鍵->ALT+C',;
      NAME='CMND3'
  PROCEDURE INIT 
      THISFORM.SETALL('FONTSIZE',INT_015*10,'TEXTBOX')
      THISFORM.SETALL('FONTSIZE',INT_015*10,'LABEL')      
      THISFORM.SETALL('FONTSIZE',INT_015*10,'COMMANDBUTTON')    
      DO CASE  
             CASE KEY()='F01'     
                        THISFORM.LABEL1.CAPTION='起始員工編號' 
                        THISFORM.LABEL2.CAPTION='截止員工編號' 
                        THISFORM.TEXT1.VALUE=S14B_TEMP.F01
                        THISFORM.TEXT2.VALUE=S14B_TEMP.F01
             CASE KEY()='F03'    
                        THISFORM.LABEL1.CAPTION='起始部門編號' 
                        THISFORM.LABEL2.CAPTION='截止部門編號'        
                        THISFORM.TEXT1.VALUE=S14B_TEMP.F03
                        THISFORM.TEXT2.VALUE=S14B_TEMP.F03                              
      ENDCASE     
  ENDPROC    
 ****
  PROCEDURE CMND1.CLICK 
      IF  EMPTY(THISFORM.TEXT1.VALUE)
           =MESSAGEBOX('起始編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT1.SETFOCUS
  	   RETURN 
      ENDIF
      IF  EMPTY(THISFORM.TEXT2.VALUE)
           =MESSAGEBOX('截止編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT2.SETFOCUS
  	   RETURN 
      ENDIF      
      DO CASE  
             CASE KEY()='F01'     
                        SELECT  * FROM S14B_TEMP WHERE F01>=THISFORM.TEXT1.VALUE AND F01<=THISFORM.TEXT2.VALUE  ORDER BY F01 NOWAIT
		        IF  _TALLY >0
		             REPORT FORM ALLTRIM(INT_116)+'S14B_1' TO PRINT PROMPT   PREVIEW
		        ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                          
             CASE KEY()='F03'    
                        SELECT  * FROM S14B_TEMP WHERE F03>=THISFORM.TEXT1.VALUE AND F03<=THISFORM.TEXT2.VALUE  ORDER BY F03 NOWAIT               
		        IF  _TALLY >0
		             REPORT FORM ALLTRIM(INT_116)+'S14B_2' TO PRINT PROMPT   PREVIEW  
		        ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                               
      ENDCASE         
           THISFORM.CMND3.CLICK
  ENDPROC      
 *** 
  PROCEDURE CMND2.CLICK 
      IF  EMPTY(THISFORM.TEXT1.VALUE)
           =MESSAGEBOX('起始編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT1.SETFOCUS
  	   RETURN 
      ENDIF
      IF  EMPTY(THISFORM.TEXT2.VALUE)
           =MESSAGEBOX('截止編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT2.SETFOCUS
  	   RETURN 
      ENDIF      
      FILENAME='S14B 出勤年度資料'+YEAR_DATE+'年'      
      DO CASE  
             CASE KEY()='F01'   
                        SELECT F01 員工編號,F02 員工姓名,F03 部門編號,F04 部門名稱,F05 年度遲到總分鐘數,F06 年度早退總分鐘數,F09 年度平日加班總小時數,F10 年度假日加班總小時數,F09+F10 年度加班總小時數;
                        FROM S14B_TEMP WHERE F01>=THISFORM.TEXT1.VALUE AND F01<=THISFORM.TEXT2.VALUE  ORDER BY F01 NOWAIT
		        IF  _TALLY >0
	                     GCDELIMFILE=PUTFILE('儲存路徑',FILENAME,'XLS')
			     IF !EMPTY(GCDELIMFILE) 
			          ON ERROR DO FILE_IMPACT
			          COPY TO (GCDELIMFILE) TYPE XL5     
			          =MESSAGEBOX('儲存成功',0+64,'提示訊息視窗') 
			     ELSE
		                 THISFORM.TEXT1.SETFOCUS
		                 RETURN			     
			     ENDIF 
		        ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                          
             CASE KEY()='F03'    
                        SELECT F03 部門編號,F04 部門名稱,F01 員工編號,F02 員工姓名,F05 年度遲到總分鐘數,F06 年度早退總分鐘數,F09 年度平日加班總小時數,F10 年度假日加班總小時數,F09+F10 年度加班總小時數;
                        FROM S14B_TEMP WHERE F03>=THISFORM.TEXT1.VALUE AND F03<=THISFORM.TEXT2.VALUE  ORDER BY F03 NOWAIT               
		        IF  _TALLY >0
	                     GCDELIMFILE=PUTFILE('儲存路徑',FILENAME,'XLS')
			     IF !EMPTY(GCDELIMFILE) 
			          ON ERROR DO FILE_IMPACT
			          COPY TO (GCDELIMFILE) TYPE XL5     
			          =MESSAGEBOX('儲存成功',0+64,'提示訊息視窗') 
			     ELSE
		                 THISFORM.TEXT1.SETFOCUS
		                 RETURN			     
			     ENDIF  
		        ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                               
      ENDCASE         
           THISFORM.CMND3.CLICK
  ENDPROC    
  ***       
  PROCEDURE CMND3.CLICK
      THISFORM.RELEASE
  ENDPROC      
  **
  PROC CMND1.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND1.TOP=INT_015*15
             THISFORM.CMND1.LEFT=INT_015*191
  ENDPROC     
  PROC CMND1.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND1.TOP=INT_015*14
             THISFORM.CMND1.LEFT=INT_015*190                  
  ENDPROC     
  PROC CMND2.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND2.TOP=INT_015*46
             THISFORM.CMND2.LEFT=INT_015*191
  ENDPROC     
  PROC CMND2.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND2.TOP=INT_015*45
             THISFORM.CMND2.LEFT=INT_015*190                  
  ENDPROC     
  PROC CMND3.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND3.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND3.TOP=INT_015*77
             THISFORM.CMND3.LEFT=INT_015*191
  ENDPROC     
  PROC CMND3.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND3.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND3.TOP=INT_015*76
             THISFORM.CMND3.LEFT=INT_015*190                  
  ENDPROC 
ENDDEFINE    
************************************************缺勤單位  
FUNC ABSENCE_CLASS
     PARA JUK
     JO=''
     DO CASE
        CASE JUK='D'
             JO='日'
        CASE JUK='H'
             JO='小時' 
        CASE JUK='S'
             JO='次/月' 
     ENDCASE
     RETURN JO   