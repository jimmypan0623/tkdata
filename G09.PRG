***程式名稱:G09.庫存異動紀錄
SET EXCL OFF
*!*	G15='G15'+LEFT(DTOS(DATE()),6)   &&月報表檔
*!*	IF FILE(G15+'.DBF')=.T.
*!*		IF !USED('&G15')
*!*		   SELE 0
*!*		   USE (G15) ALIA G15 
*!*		ELSE
*!*		   SELE G15
*!*		ENDIF
*!*		SET ORDER TO 1  
*!*		IF RECCOUNT('G15')=0
*!*			=MESSAGEBOX(STR(VAL(LEFT(DTOS(DATE()),4))-1911)+'年'+RIGHT(LEFT(DTOS(DATE()),6),2)+'月之月加權平均成本(G08)尚未計算,請計算後再執行',0+64,'提示訊息視窗')
*!*			SELE G15
*!*			USE
*!*		RETURN           
*!*		ENDIF
*!*	ELSE
*!*		=MESSAGEBOX('月份檔尚未建立故無資料,請重新輸入!',0+64,'提示視窗')
*!*		RETURN
*!*	ENDIF
CLOSE ALL
CLEAR 
FLG='0'
FCH=''
CHK=''
CHK_STR=''
R=0
QTY=0
TQTY=0
AREA1='G15_1'
AREA2='G15'
IF !USED('A23')
    SELE 0
    USE A23
ELSE 
   SELE A23
ENDIF
SET ORDER TO 1         
SELE A23
SET ORDER TO 1
IF ! USED('A01')
   SELE 0
   USE A01 INDE A01
ELSE
   SELE A01
ENDIF      
SET ORDER TO A01
IF !USED('A02')
   SELE 0
   USE A02 INDE A02
ELSE
   SELE A02
ENDIF
SET ORDER TO A021     
SEEK sys_oper+'G09'
IF !USED('B01')
   SELE 0
   USE B01 INDE B01
ELSE
   SELE B01
ENDIF
SET ORDER TO 1          
G15='G15'+LEFT(DTOS(DATE()),6)
G151='G151'+LEFT(DTOS(DATE()),6)
IF !USED('&G15')
   SELE 0
   USE (G15) ALIA G15
ELSE
   SELE G15
ENDIF
SET ORDER TO 1     
CREATE CURSOR G15_1;
(F01 C(43))
INDE ON F01 TAG G15_11
SELE G15.F01  FROM G15  GROUP BY G15.F01 ORDER BY G15.F01 INTO CURSOR G15_2 WHERE G15.F01=ANY(SELE F01 FROM B01 WHERE  !EMPTY(B01.F98))
SELE G15_2
GO TOP
DO WHILE !EOF()
   SELE G15_1
   APPEND BLANK
   REPL F01 WITH G15_2.F01
   SELE G15_2
   SKIP   
ENDDO
SELE G15_2
USE   
SELE G15_1
SET ORDER TO 1
SET RELATION TO F01 INTO B01
GO TOP
********
G09FORM=CREATEOBJECT("TKG09")
G09FORM.SHOW  
DEFINE CLASS TKG09 AS FORM
  CAPTION='G09.庫存明細帳'
  CONTROLBOX=.F.
  BORDERSTYLE=3  
  MAXBUTTON=.F.
  MINBUTTON=.F.
  MOVABLE=.F.
  CLOSABLE=.F.
  CONTROLCOUNT=57
  FONTSIZE=INT_015*9
  HEIGHT=INT_015*550
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WIDTH=INT_015*789
  WINDOWTYPE=1
  NAME='TKG09'
  ADD OBJECT SHAPE1 AS SHAPE1 WITH;
      AUTOCENTER=.T.
  ADD OBJECT LBL_REC AS LBL_REC WITH;
      AUTOCENTER =.T.
  ADD OBJECT REC_COUNT AS REC_COUNT  WITH;
      CAPTION=STR(RECCOUNT())        
  ADD OBJECT SHAPE2 AS SHAPE2 WITH;
      AUTOCENTER=.T.
  ADD OBJECT SHAPE3 AS SHAPE3 WITH;
      AUTOCENTER=.T.
  ADD OBJECT PAGEFRAME1 AS PAGEFRAME WITH;
      LEFT=INT_015*236,;
      WIDTH=INT_015*540,;
      HEIGHT=INT_015*200,;
      FONTSIZE=INT_015*9,;
      PAGECOUNT=2,;
      TABSTRETCH=1,;
      ACTIVEPAGE=1,;
      VISIBLE=.T.,;
      PAGE1.NAME='PAGE1',;
      PAGE1.VISIBLE=.T.,;
      PAGE1.CAPTION='表頭資料',;
      PAGE2.NAME='PAGE2',;
      PAGE2.VISIBLE=.T.,;
      PAGE2.CAPTION='表身詳細資料內容'   
  ADD OBJECT KEY_LIST AS KEY_LIST WITH;
      AUTOCENTER=.T.,;
      WIDTH=INT_015*130
  ADD OBJECT MTH_LIST AS MTH_LIST WITH;
      AUTOCENTER=.T.
  ADD OBJECT GRID1 AS GRID1 WITH;
      COLUMNCOUNT=2,;            
      RECORDSOURCE='G15_1',;      
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2'      
  ADD OBJECT GRID2 AS GRID2 WITH;
      COLUMNCOUNT=7,;            
      RECORDSOURCE='G15',; 
      LINKMASTER='G15_1',;
      RELATIONALEXPR='F01',;
      CHILDORDER=G151,;  
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2',;
      COLUMN3.NAME='COLUMN3',;
      COLUMN4.NAME='COLUMN4',;
      COLUMN5.NAME='COLUMN5',;
      COLUMN6.NAME='COLUMN6',;
      COLUMN7.NAME='COLUMN7'
  ADD OBJECT CMDGROUP AS CMDGROUP WITH;
      AUTOSIZE=.T.                             
  ADD OBJECT ORPGROUP AS ORPGROUP WITH;    
      AUTOSIZE=.T. 
 **********                   
  PROCEDURE PAGEFRAME1.PAGE1.INIT     
     THIS.ADDOBJECT('LBLF01','LABEL')
     WITH THIS.LBLF01
          .VISIBLE=.T.
          .LEFT=INT_015*14
          .TOP=INT_015*13
          .WIDTH=INT_015*50
          .CAPTION='料品編號'          
     ENDWITH
     THIS.ADDOBJECT('LBLF02','LABEL')
     WITH THIS.LBLF02
         .VISIBLE=.T.
         .LEFT=INT_015*14
         .TOP=INT_015*38
         .WIDTH=INT_015*50
         .CAPTION='品名規格'
     ENDWITH    
     THIS.ADDOBJECT('LBLF021','LABEL')         
     WITH THIS.LBLF021
         .VISIBLE=.T.
         .LEFT=INT_015*65
         .TOP=INT_015*38
         .AUTOSIZE=.T.
     ENDWITH
     THIS.ADDOBJECT('LBLQTP','LABEL')
     WITH THIS.LBLQTP
         .VISIBLE=.T.
         .LEFT=INT_015*14
         .TOP=INT_015*88
         .WIDTH=INT_015*50
         .CAPTION='總入數量'
     ENDWITH    
     THIS.ADDOBJECT('LBLQTP1','LABEL')
     WITH THIS.LBLQTP1
         .VISIBLE=.T.
         .LEFT=INT_015*65
         .TOP=INT_015*88
         .AUTOSIZE=.T.
         .CAPTION='0'
     ENDWITH         
     THIS.ADDOBJECT('LBLQTM','LABEL')
     WITH THIS.LBLQTM
         .VISIBLE=.T.
         .LEFT=INT_015*14
         .TOP=INT_015*113
         .WIDTH=INT_015*50
         .CAPTION='總出數量'
     ENDWITH         
     THIS.ADDOBJECT('LBLQTM1','LABEL')
     WITH THIS.LBLQTM1
         .VISIBLE=.T.
         .LEFT=INT_015*65
         .TOP=INT_015*113
         .AUTOSIZE=.T.
         .CAPTION='0'
     ENDWITH                   
     THIS.ADDOBJECT('LBLPLS','LABEL')
     WITH THIS.LBLPLS
         .VISIBLE=.T.
         .LEFT=INT_015*314
         .TOP=INT_015*88
         .WIDTH=INT_015*50
         .CAPTION='總入金額'
     ENDWITH    
     THIS.ADDOBJECT('LBLPLS1','LABEL')
     WITH THIS.LBLPLS1
         .VISIBLE=.T.
         .LEFT=INT_015*365
         .TOP=INT_015*88
         .AUTOSIZE=.T.
         .CAPTION='0'
     ENDWITH         
     THIS.ADDOBJECT('LBLMNS','LABEL')
     WITH THIS.LBLMNS
         .VISIBLE=.T.
         .LEFT=INT_015*314
         .TOP=INT_015*113
         .WIDTH=INT_015*50
         .CAPTION='總出金額'
     ENDWITH         
     THIS.ADDOBJECT('LBLMNS1','LABEL')
     WITH THIS.LBLMNS1
         .VISIBLE=.T.
         .LEFT=INT_015*365
         .TOP=INT_015*113
         .AUTOSIZE=.T.
         .CAPTION='0'
     ENDWITH              
     THIS.ADDOBJECT('TXTF01','TEXTBOX')
     WITH THIS.TXTF01
          .VISIBLE=.T.
          .LEFT=INT_015*65
          .TOP=INT_015*11
          .WIDTH=INT_015*261
     ENDWITH          

  ENDPROC
*********     
  PROCEDURE PAGEFRAME1.PAGE2.INIT
	THIS.ADDOBJECT('LBLF02','LABEL')
	WITH THIS.LBLF02 
	     .VISIBLE=.T.
         .LEFT=INT_015*14
         .TOP=INT_015*18
	     .WIDTH=INT_015*50
         .CAPTION='異動日期'
    ENDWITH  
    THIS.ADDOBJECT('LBLF03','LABEL')
    WITH THIS.LBLF03
         .VISIBLE=.T.
	     .LEFT=INT_015*214
	     .TOP=INT_015*18
	     .WIDTH=INT_015*50
	     .CAPTION='單據類別'
	ENDWITH     
    THIS.ADDOBJECT('LBLF04','LABEL')
    WITH THIS.LBLF04
         .VISIBLE=.T.
         .LEFT=INT_015*14
	     .TOP=INT_015*43
	     .WIDTH=INT_015*50
	     .CAPTION='異動數量'
	ENDWITH         
    THIS.ADDOBJECT('LBLF05','LABEL')
    WITH THIS.LBLF05
         .VISIBLE=.T.
         .LEFT=INT_015*14
	     .TOP=INT_015*68
	     .WIDTH=INT_015*50
	     .CAPTION='單        價'
	ENDWITH         	
    THIS.ADDOBJECT('LBLFTL','LABEL')
    WITH THIS.LBLFTL
         .VISIBLE=.T.
         .LEFT=INT_015*14
	     .TOP=INT_015*93
	     .WIDTH=INT_015*50
	     .CAPTION='金額小計'
	ENDWITH         		
    THIS.ADDOBJECT('LBLF06','LABEL')
    WITH THIS.LBLF06
         .VISIBLE=.T.    
         .LEFT=INT_015*14
         .TOP=INT_015*118
         .WIDTH=INT_015*50
         .CAPTION='累計數量'       
    ENDWITH    
    THIS.ADDOBJECT('LBLF07','LABEL')
    WITH THIS.LBLF07
         .VISIBLE=.T.    
         .LEFT=INT_015*14
         .TOP=INT_015*143
         .WIDTH=INT_015*50
         .CAPTION='備        註'       
    ENDWITH        
    THIS.ADDOBJECT('TXTF02','TEXTBOX')
	WITH THIS.TXTF02     
	     .VISIBLE=.T.
	     .LEFT=INT_015*65
	     .TOP=INT_015*14
	     .WIDTH=INT_015*30
	     .HEIGHT=INT_015*25
	     .NAME='TXTF02'  
	ENDWITH     
    THIS.ADDOBJECT('TXTF03','TEXTBOX')
    WITH THIS.TXTF03
         .VISIBLE=.T.           
         .LEFT=INT_015*265
         .TOP=INT_015*14
         .WIDTH=INT_015*100
         .HEIGHT=INT_015*25
         .NAME='TXTF03'
    ENDWITH         	
	THIS.ADDOBJECT('TXTF04','TEXTBOX')
	WITH THIS.TXTF04      
	     .VISIBLE=.T.
	     .LEFT=INT_015*65
	     .TOP=INT_015*39
	     .WIDTH=INT_015*100
	     .HEIGHT=INT_015*25
	     .NAME='TXTF04'  
	ENDWITH             
     THIS.ADDOBJECT('TXTF05','TEXTBOX')
	WITH THIS.TXTF05
	     .VISIBLE=.T.
	     .LEFT=INT_015*65
	     .TOP=INT_015*64
	     .WIDTH=INT_015*100
	     .HEIGHT=INT_015*25
	     .NAME='TXTF05'	    
	ENDWITH     
     THIS.ADDOBJECT('TXTFTL','TEXTBOX')
	WITH THIS.TXTFTL
	     .VISIBLE=.T.
	     .LEFT=INT_015*65
	     .TOP=INT_015*89
	     .WIDTH=INT_015*100
	     .HEIGHT=INT_015*25
	     .NAME='TXTFTL'	    
	ENDWITH     	
    THIS.ADDOBJECT('TXTF06','TEXTBOX')
    WITH THIS.TXTF06
         .VISIBLE=.T.           
         .LEFT=INT_015*65
         .TOP=INT_015*114
         .WIDTH=INT_015*100
         .HEIGHT=INT_015*25
         .NAME='TXTF06'
    ENDWITH     
    THIS.ADDOBJECT('TXTF07','TEXTBOX')
    WITH THIS.TXTF07
         .VISIBLE=.T.           
         .LEFT=INT_015*65
         .TOP=INT_015*139
         .WIDTH=INT_015*373
         .HEIGHT=INT_015*25
         .NAME='TXTF07'
    ENDWITH             
  ENDPROC
*********         
  PROCEDURE PAGEFRAME1.PAGE1.ACTIVATE
     R=0
     SELE G15_1
     THISFORM.GRID1.ENABLED=.T.   
     THISFORM.GRID1.SETFOCUS   
     THISFORM.KEY_LIST.ENABLED=.T.
     THISFORM.MTH_LIST.ENABLED=.T.
    IF THISFORM.GRID1.ACTIVEROW=0 
        THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.F.      
        THISFORM.CMDGROUP.UP_BOTT.ENABLED=.F.
        THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.F.
        THISFORM.CMDGROUP.END_BOTT.ENABLED=.F.
        THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
        THISFORM.ORPGROUP.PNT_BOTT.ENABLED=.F.
        THISFORM.PAGEFRAME1.PAGE1.LBLF021.CAPTION='' 
        THISFORM.PAGEFRAME1.PAGE1.LBLQTP1.CAPTION='0'
        THISFORM.PAGEFRAME1.PAGE1.LBLQTM1.CAPTION='0'            
        THISFORM.PAGEFRAME1.PAGE1.LBLPLS1.CAPTION='0'
        THISFORM.PAGEFRAME1.PAGE1.LBLMNS1.CAPTION='0'        
        THISFORM.PAGEFRAME1.PAGE2.ENABLED=.F.      
     ELSE
        THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.T.      
        THISFORM.CMDGROUP.UP_BOTT.ENABLED=.T.
        THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.T.
        THISFORM.CMDGROUP.END_BOTT.ENABLED=.T.
        THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.
        THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)    &&判斷有無列印的權限  
        THISFORM.PAGEFRAME1.PAGE2.ENABLED=.T.
     ENDIF   
  ENDPROC
 ********** 
  PROCEDURE PAGEFRAME1.PAGE2.ACTIVATE
       SELE G15
        THISFORM.GRID2.READONLY=.T.   
        THISFORM.GRID2.SETFOCUS       
     IF THISFORM.GRID2.ACTIVEROW=0 
        THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.F.      
        THISFORM.CMDGROUP.UP_BOTT.ENABLED=.F.
        THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.F.
        THISFORM.CMDGROUP.END_BOTT.ENABLED=.F.
        THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
        THISFORM.PAGEFRAME1.PAGE2.SETALL('VALUE','','TEXTBOX')
        THISFORM.PAGEFRAME1.PAGE2.TXTF04.VALUE=0
        THISFORM.PAGEFRAME1.PAGE2.TXTF05.VALUE=0
        THISFORM.PAGEFRAME1.PAGE2.TXTF06.VALUE=0
     ELSE
        THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.T.      
        THISFORM.CMDGROUP.UP_BOTT.ENABLED=.T.
        THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.T.
        THISFORM.CMDGROUP.END_BOTT.ENABLED=.T.
        THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.
        THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)  &&&&判斷有無列印的權限  
     ENDIF   
     R=RECNO('&AREA1')
     THISFORM.GRID1.SETALL("DYNAMICBACKCOLOR","IIF(RECNO('&AREA1')=R,RGB(255,255,0),'')","COLUMN")
     THISFORM.GRID1.ENABLED=.F.     
     THISFORM.KEY_LIST.ENABLED=.F.     
     THISFORM.MTH_LIST.ENABLED=.F.     
  ENDPROC 
*********      
  PROC INIT       
       THISFORM.ORPGROUP.NEW_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.EDIT_BOTT.VISIBLE=.F.       
       THISFORM.ORPGROUP.DEL_BOTT.VISIBLE=.F.  
       THISFORM.SETALL('HEIGHT',INT_015*25,'TEXTBOX')
       THISFORM.SETALL('HEIGHT',INT_015*17,'LABEL')
       THISFORM.SETALL('FONTSIZE',INT_015*9,'TEXTBOX')
       THISFORM.SETALL('FONTSIZE',INT_015*9,'LABEL')       
       THISFORM.SETALL('READONLY',.T.,'TEXTBOX')
       THISFORM.GRID1.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)
       THISFORM.GRID2.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'HEADER')         
       THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='料品編號'
       THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='品名規格'
       THISFORM.GRID1.COLUMN1.CONTROLSOURCE='G15_1.F01'
       THISFORM.GRID1.COLUMN2.CONTROLSOURCE='B01.F02'
       THISFORM.GRID1.COLUMN1.WIDTH=INT_015*149
       THISFORM.GRID1.COLUMN2.WIDTH=INT_015*149
       THISFORM.GRID2.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID2.SETALL('FONTSIZE',INT_015*9,'HEADER')       
       THISFORM.GRID2.COLUMN1.HEADER1.CAPTION='日期'       
	   THISFORM.GRID2.COLUMN2.HEADER1.CAPTION='單據類別'	  
	   THISFORM.GRID2.COLUMN3.HEADER1.CAPTION='異動數量'
       THISFORM.GRID2.COLUMN4.HEADER1.CAPTION='單        價'	   
	   THISFORM.GRID2.COLUMN5.HEADER1.CAPTION='金額小計'
	   THISFORM.GRID2.COLUMN6.HEADER1.CAPTION='累計數量'	   
       THISFORM.GRID2.COLUMN7.HEADER1.CAPTION='備註說明'
	   THISFORM.GRID2.LINKMASTER='G15_1'
       THISFORM.GRID2.RELATIONALEXPR='F01'
	   THISFORM.GRID2.COLUMN1.CONTROLSOURCE='G15.F02'
	   THISFORM.GRID2.COLUMN2.CONTROLSOURCE="IIF(G15.F04<>0,BILL(G15.F03),STUFF(BILL(G15.F03),5,4,'折讓'))"
	   THISFORM.GRID2.COLUMN3.CONTROLSOURCE='G15.F04'
	   THISFORM.GRID2.COLUMN4.CONTROLSOURCE='G15.F05'
  	   THISFORM.GRID2.COLUMN5.CONTROLSOURCE='IIF(G15.F04=0,ROUND(G15.F05,INT_068),ROUND(G15.F04*G15.F05,INT_068))'
  	   THISFORM.GRID2.COLUMN6.CONTROLSOURCE='G15.F06'  	   
   	   THISFORM.GRID2.COLUMN7.CONTROLSOURCE='G15.F07'
	   THISFORM.GRID2.COLUMN1.WIDTH=INT_015*25
       THISFORM.GRID2.COLUMN2.WIDTH=INT_015*100
       THISFORM.GRID2.COLUMN3.WIDTH=INT_015*80
       THISFORM.GRID2.COLUMN4.WIDTH=INT_015*100
       THISFORM.GRID2.COLUMN5.WIDTH=INT_015*100
       THISFORM.GRID2.COLUMN6.WIDTH=INT_015*100       
       THISFORM.GRID2.COLUMN7.WIDTH=INT_015*200
       THISFORM.GRID1.READONLY=.T.      
       THISFORM.GRID2.READONLY=.T.            
       THISFORM.GRID1.SETFOCUS
       IF THISFORM.GRID1.ACTIVEROW=0
          THISFORM.CMDGROUP.ENABLED=.F.
       ELSE
          THISFORM.CMDGROUP.ENABLED=.T.       
       ENDIF          
       JK='料品編號'     
       CHK_STR=THISFORM.MTH_LIST.DISPLAYVALUE        
  ENDPROC            
***********     
  PROC KEY_LIST.INIT 
       WITH THIS
            .ADDITEM('依料號排列')                            
       ENDWITH      
  ENDPROC 
*********  
  PROC KEY_LIST.INTERACTIVECHANGE    

  ENDPROC
*********    
  PROC MTH_LIST.INIT        
       WITH THIS
            SELE A23
            GO TOP
            K=0
            S=0
            DO WHILE !EOF()                
               K=K+1
               IF INT_012=1
                   SHDT=PADL(ALLTRIM(STR(VAL(LEFT(DTOS(A23.F01),4))-1911)),4,'0')+'/'+SUBSTR(DTOS(A23.F01),5,2)
               ELSE
                   SHDT=LEFT(DTOS(A23.F01),4)+'/'+SUBSTR(DTOS(A23.F01),5,2)
               ENDIF    
               .ADDITEM(SHDT) &&               
               IF LEFT(DTOS(DATE()),6)=LEFT(DTOS(A23.F01),6)
                   S=K
               ENDIF       
               SKIP
            ENDDO   
            .VALUE=S
       ENDWITH   
  ENDPROC   
**********       
  PROC MTH_LIST.INTERACTIVECHANGE
       CHK_STR=THIS.DISPLAYVALUE         
       THISFORM.GRID1.RECORDSOURCE=''
       THISFORM.GRID2.RECORDSOURCE=''
       THISFORM.GRID2.LINKMASTER=''
       THISFORM.GRID2.RELATIONALEXPR=''
       THISFORM.GRID2.CHILDORDER=''          
       SELE G15
       SET RELA OFF INTO G15_1
       USE
       SELE G15_1
       SET RELATION OFF INTO B01
       USE     
       G15='G15'+LEFT(DTOS(CTOD(THIS.DISPLAYVALUE+'/01')),6)
       G151='G151'+LEFT(DTOS(CTOD(THIS.DISPLAYVALUE+'/01')),6)
       IF !USED('&G15')
          SELE 0
          USE (G15) ALIA G15
       ELSE
          SELE G15
       ENDIF
       SET ORDER TO 1      
       CREATE CURSOR G15_1;
       (F01 C(43))
       INDE ON F01 TAG G15_11
       SELE G15.F01 FROM G15 GROUP BY G15.F01 ORDER BY G15.F01 INTO CURSOR G15_2 WHERE G15.F01=ANY(SELE F01 FROM B01 WHERE !EMPTY(B01.F98))
       SELE G15_2
       GO TOP
       DO WHILE !EOF()
          SELE G15_1
          APPEND BLANK
          REPL F01 WITH G15_2.F01
          SELE G15_2
          SKIP   
       ENDDO
       SELE G15_2
       USE   
       SELE G15_1
       SET ORDER TO 1
       SET RELATION TO F01 INTO B01
       GO TOP
       THISFORM.GRID1.RECORDSOURCE='G15_1'
       THISFORM.GRID2.RECORDSOURCE='G15'
       THISFORM.GRID2.LINKMASTER='G15_1'
       THISFORM.GRID2.RELATIONALEXPR='F01'      
       THISFORM.GRID2.CHILDORDER=G151
       THISFORM.GRID1.COLUMN1.CONTROLSOURCE='G15_1.F01'
       THISFORM.GRID1.COLUMN2.CONTROLSOURCE='B01.F02'       
       THISFORM.GRID2.COLUMN1.CONTROLSOURCE='G15.F02'
       THISFORM.GRID2.COLUMN2.CONTROLSOURCE="IIF(G15.F04<>0,BILL(G15.F03),STUFF(BILL(G15.F03),5,4,'折讓'))"
       THISFORM.GRID2.COLUMN3.CONTROLSOURCE='G15.F04'
       THISFORM.GRID2.COLUMN4.CONTROLSOURCE='G15.F05'
       THISFORM.GRID2.COLUMN5.CONTROLSOURCE='IIF(G15.F04=0,ROUND(G15.F05,INT_068),ROUND(G15.F04*G15.F05,INT_068))'
       THISFORM.GRID2.COLUMN6.CONTROLSOURCE='G15.F06' 
       THISFORM.GRID2.COLUMN7.CONTROLSOURCE='G15.F07'                   
       THISFORM.PAGEFRAME1.ACTIVEPAGE=1
       THISFORM.REFRESH
       THISFORM.GRID1.SETFOCUS        
       IF THISFORM.GRID1.ACTIVEROW=0
          THISFORM.CMDGROUP.ENABLED=.F.
          THISFORM.ORPGROUP.PNT_BOTT.ENABLED=.F.
          THISFORM.PAGEFRAME1.PAGE1.LBLF021.CAPTION='' 
          THISFORM.PAGEFRAME1.PAGE1.LBLQTP1.CAPTION='0'
          THISFORM.PAGEFRAME1.PAGE1.LBLQTM1.CAPTION='0'            
          THISFORM.PAGEFRAME1.PAGE1.LBLPLS1.CAPTION='0'
          THISFORM.PAGEFRAME1.PAGE1.LBLMNS1.CAPTION='0'          
       ELSE
          THISFORM.CMDGROUP.ENABLED=.T.      
          THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限    
       ENDIF          
       FLG='0'
  ENDPROC
**********  
  PROC GRID1.AFTERROWCOLCHANGE
       LPARAMETERS NCOLINDEX       
       THISFORM.PAGEFRAME1.PAGE1.TXTF01.VALUE=G15_1.F01
       THISFORM.PAGEFRAME1.PAGE1.LBLF021.CAPTION=B01.F02    
       THISFORM.PAGEFRAME1.PAGE1.LBLQTP1.CAPTION=TRANSFORM(QTYPL(G15_1.F01),'@R 999999999999.99')       
       THISFORM.PAGEFRAME1.PAGE1.LBLQTM1.CAPTION=TRANSFORM(QTYMN(G15_1.F01),'@R 999999999999.99')              
       THISFORM.PAGEFRAME1.PAGE1.LBLPLS1.CAPTION=TRANSFORM(ROUND(PLUS(G15_1.F01),INT_068),'@R 999999999999.99')       
       THISFORM.PAGEFRAME1.PAGE1.LBLMNS1.CAPTION=TRANSFORM(ROUND(MINS(G15_1.F01),INT_068),'@R 999999999999.99')              
       FCH=THISFORM.ACTIVECONTROL.NAME
       JK=THIS.COLUMN1.HEADER1.CAPTION  
       IF THIS.ACTIVEROW=0
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
       ELSE
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.
       ENDIF          
       CHK=''       
*       THISFORM.REFRESH
  ENDPROC
**********       
  PROC GRID2.AFTERROWCOLCHANGE
       LPARAMETERS NCOLINDEX
       THISFORM.PAGEFRAME1.PAGE2.TXTF02.VALUE=G15.F02       
       THISFORM.PAGEFRAME1.PAGE2.TXTF03.VALUE=IIF(G15.F04<>0,BILL(G15.F03),STUFF(BILL(G15.F03),5,4,'折讓'))
       THISFORM.PAGEFRAME1.PAGE2.TXTFTL.VALUE=IIF(G15.F04=0,ROUND(G15.F05,INT_068),ROUND(G15.F04*G15.F05,INT_068))
       THISFORM.PAGEFRAME1.PAGE2.TXTF04.VALUE=G15.F04       
       THISFORM.PAGEFRAME1.PAGE2.TXTF06.VALUE=G15.F06
       THISFORM.PAGEFRAME1.PAGE2.TXTF07.VALUE=G15.F07
       THISFORM.PAGEFRAME1.PAGE2.TXTF05.VALUE=G15.F05
       FCH=THISFORM.ACTIVECONTROL.NAME
       JK=THIS.COLUMN1.HEADER1.CAPTION
       CHK=THISFORM.PAGEFRAME1.PAGE1.TXTF01.VALUE
       IF THISFORM.PAGEFRAME1.ACTIVEPAGE=1
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
       ENDIF   
*       THISFORM.REFRESH
  ENDPROC 
ENDDEFINE    
***********************************************列印程序
PROCEDURE PNT_PRC  
     LK=G15_1.F01      
      QTY1=QTYPL(LK)
      QTY2=QTYMN(LK)
      PRICE1=PLUS(LK)
      PRICE2=MINS(LK) 
      SELECT G15.F02,;
             G15.F03,;
             G15.F04,;
             G15.F05,;  
             G15.F06,;                  
             G15.F07,;
             B01.F02;  
     FROM G15,G15_1,B01 WHERE  G15.F01=LK AND G15_1.F01=LK AND B01.F01=G15_1.F01; 
     ORDER BY G15.F02 NOWAIT
     IF _TALLY >0
        REPORT FORM ALLTRIM(INT_116)+'G09' PREVIEW
*         REPORT FORM G15_1TEST TO PRINT PROMPT 
     ENDIF  
ENDPROC
************************************************單據類別     
FUNC BILL
     PARA JUK
     JO=''
     DO CASE
        CASE JUK=' '
             JO='期初'
        CASE JUK='A'
             JO='進貨單'
        CASE JUK='B'
             JO='進貨退出單'
        CASE JUK='C'
             JO='出貨單'
        CASE JUK='D'
             JO='出貨退回單'
        CASE JUK='E'
             JO='移轉單'
        CASE JUK='H'
             JO='盤差調整單'
        CASE JUK='I'
             JO='報廢單'             
        CASE JUK='K'
             JO='領用出倉單'
        CASE JUK='L'
             JO='退回入倉單'
        CASE JUK='M'
             JO='委外入庫單'             
        CASE JUK='N'
             JO='進貨檢驗單'
        CASE JUK='Q'
             JO='額外生產耗料單'             
        CASE JUK='T'
             JO='完工入庫單'  
        CASE JUK='R'
             JO='製品退出單'               
        CASE JUK='G'
             JO='客供品進貨單'                                                                              
        CASE JUK='J'
             JO='客供品進貨退出單'
        CASE JUK='Z'
             JO='樣品領用單'     
        CASE JUK='F'
             JO='樣品申請單(廠商)'        
     ENDCASE
     RETURN JO                  
*******************************************計算正數總金額
FUNC PLUS
     PARA PL
     SELE G15
     SEEK PL
     K=0
     IF FOUND()
        DO WHILE G15.F01=PL     
           DO CASE 
              CASE G15.F04>0 AND G15.F05<>0
                   K=K+ROUND(G15.F04*G15.F05,INT_068)
              CASE G15.F04=0 AND G15.F05>0
                   K=K+G15.F05     
            ENDCASE
            SKIP
        ENDDO
      ENDIF
      RETURN K                 
****************************************計算負數總金額   
FUNC MINS
     PARA MN
     SELE G15
     SEEK MN
     L=0
     IF FOUND()
        DO WHILE G15.F01=MN 
           DO CASE 
              CASE G15.F04<0 AND G15.F05<>0
                   L=L+ROUND(G15.F04*G15.F05,INT_069)
              CASE G15.F04=0 AND G15.F05<0
                   L=L+G15.F05     
            ENDCASE
            SKIP
        ENDDO
      ENDIF
      RETURN L                 
***************************************計算正數數量      
FUNC QTYPL
     PARA YPL
     SELE G15
     SEEK YPL
     M=0
     IF FOUND()
        DO WHILE G15.F01=YPL     
           IF G15.F04>0
              M=M+G15.F04     
           ENDIF
           SKIP
        ENDDO
      ENDIF
      RETURN M                            
***************************************計算負數數量      
FUNC QTYMN
     PARA YMN
     SELE G15
     SEEK YMN
     N=0
     IF FOUND()
        DO WHILE G15.F01=YMN     
           IF G15.F04<0
              N=N+G15.F04     
           ENDIF
           SKIP
        ENDDO
      ENDIF
      RETURN N                                  