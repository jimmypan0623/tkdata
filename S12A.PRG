**程式名稱:S12A.薪資所得明細查詢
SET EXCL OFF
CLOSE ALL
CLEAR 
AREA1='S12A_TEMP'
FLG='0'
FCH=''
CHK=''
STAR_DATE=''		&&查詢起始日期
END_DATE=''			&&查詢截止日起
IF !USED('A01')
   SELE 0
   USE A01
ELSE 
   SELE A01
ENDIF
SET ORDER TO A01      
IF !USED('S05')
   SELE 0
   USE S05 
ELSE
   SELE S05
ENDIF
SET ORDER TO S051
IF !USED('A02')
   SELE 0
   USE A02 INDE A02
ELSE
   SELE A02
ENDIF
SET ORDER TO A021     
SEEK SYS_OPER+'S05'  
IF FOUND()  AND A02.F09='*'   &&判斷是否有權限可處理所有員工
    SELECT F01 FROM S05 INTO CURSOR S05_TEMP
ELSE
    SELECT F01 FROM S05 WHERE F27=.T. INTO CURSOR S05_TEMP
ENDIF
SELECT A02      
SEEK SYS_OPER+'S12'    
IF !USED('A23')		&&月份檔
   SELE 0
   USE A23 INDE A23
ELSE
   SELE A23
ENDIF
SELECT A23
GO TOP
FIRST_DATE=LEFT(DTOS(A23.F01),4)+'/'+SUBSTR(DTOS(A23.F01),5,2) &&系統月份檔第一筆開帳月份
GO BOTTOM
LAST_DATE=LEFT(DTOS(A23.F01),4)+'/'+SUBSTR(DTOS(A23.F01),5,2)  &&系統月份檔最後一筆開帳月份
IF !USED('S11')
   SELE 0
   USE S11 
ELSE
   SELE S11
ENDIF
SET ORDER TO S111
*********
S12AFORM=CREATEOBJECT("TKS12A")
S12AFORM.SHOW  
DEFINE CLASS TKS12A AS FORM
*!*	  AUTOCENTER=.T.
  CAPTION='S12A.薪資所得明細查詢  (請輸入西元年月  例如:2005/08)'
  TOP=INT_015*200
  LEFT=INT_015*235    
  FONTSIZE=INT_015*9
  HEIGHT=INT_015*100
  WIDTH=INT_015*310
  MAXBUTTON=.F.
  MINBUTTON=.F.
  MOVABLE=.F.  
  CONTROLBOX=.F.
  BORDERSTYLE=1
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WINDOWTYPE=1
  NAME='TKS12A'
  ADD OBJECT LBLF01 AS LABEL WITH;
      LEFT=INT_015*45,;
      TOP=INT_015*25,;
      WIDTH=INT_015*50,;
      CAPTION='起始日期'
  ADD OBJECT TXTF01 AS TEXTBOX WITH;
      LEFT=INT_015*100,;
      TOP=INT_015*20,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*60,;
      MAXLENGTH=7,;
      INPUTMASK='9999/99',;   
      NAME='TXTF01'      
  ADD OBJECT LBLF02 AS LABEL WITH;
      LEFT=INT_015*45,;
      TOP=INT_015*60,;
      WIDTH=INT_015*50,;
      CAPTION='截止日期'        
  ADD OBJECT TXTF02 AS TEXTBOX WITH;
      LEFT=INT_015*100,;
      TOP=INT_015*55,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*60,;
      MAXLENGTH=7,;
      INPUTMASK='9999/99',;   
      NAME='TXTF02'    
  ADD OBJECT CMND1 AS COMMANDBUTTON WITH;
      LEFT=INT_015*220,;
      TOP=INT_015*20,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*50,;
      FONTSIZE=INT_015*10,;
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<Y.確認',;
      TOOLTIPTEXT='確認所輸入之內容->ALT+Y',;
      NAME='CMND1'
 ADD OBJECT CMND2 AS COMMANDBUTTON WITH;
      LEFT=INT_015*220,;
      TOP=INT_015*55,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*50,;
      FONTSIZE=INT_015*10,;
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<C.取消',;
      TOOLTIPTEXT='取消此畫面!快速鍵->ALT+C'
      NAME='CMND2'
********************
 PROCEDURE INIT 
         THISFORM.SETALL('FONTSIZE',INT_015*10,'LABEL')
         THISFORM.SETALL('FONTSIZE',INT_015*10,'TEXTBOX')
         THISFORM.SETALL('FONTSIZE',INT_015*10,'COMMANDBUTTON')
         THISFORM.TXTF01.SETFOCUS
 ENDPROC   
********    
 PROC CMND1.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND1.TOP=INT_015*21
             THISFORM.CMND1.LEFT=INT_015*221
  ENDPROC     
  PROC CMND1.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND1.TOP=INT_015*20
             THISFORM.CMND1.LEFT=INT_015*220             
  ENDPROC 
 PROC CMND2.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND2.TOP=INT_015*56
             THISFORM.CMND2.LEFT=INT_015*221
  ENDPROC     
  PROC CMND2.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND2.TOP=INT_015*55
             THISFORM.CMND2.LEFT=INT_015*220            
  ENDPROC   
********確認鍵******
PROCEDURE CMND1.CLICK   
      IF  LEN(ALLTRIM(THISFORM.TXTF01.VALUE))=1 
	   =MESSAGEBOX('起始日期不得為空白,請重新輸入!',0+64,'提示視窗')
	  THISFORM.TXTF01.SETFOCUS
	  RETURN
       ENDIF             
       IF LEN(ALLTRIM((THISFORM.TXTF01.VALUE)))<>7 OR VAL(RIGHT(ALLTRIM(THISFORM.TXTF01.VALUE),2))<=0 OR VAL(RIGHT(ALLTRIM(THISFORM.TXTF01.VALUE),2))>=13
             =MESSAGEBOX('起始日期不正確',0+64,'提示視窗')
            THISFORM.TXTF01.SETFOCUS
             RETURN
       ENDIF
      IF  LEN(ALLTRIM(THISFORM.TXTF02.VALUE))=1 
	   =MESSAGEBOX('截止日期不得為空白,請重新輸入!',0+64,'提示視窗')
	  THISFORM.TXTF02.SETFOCUS
	  RETURN
       ENDIF         
       IF LEN(ALLTRIM((THISFORM.TXTF02.VALUE)))<>7 OR VAL(RIGHT(ALLTRIM(THISFORM.TXTF02.VALUE),2))<=0 OR VAL(RIGHT(ALLTRIM(THISFORM.TXTF02.VALUE),2))>=13
           =MESSAGEBOX('截止日期不正確',0+64,'提示視窗')
           THISFORM.TXTF02.SETFOCUS
           RETURN
       ENDIF 	 
       IF CTOD(THISFORM.TXTF01.VALUE+'/01')>CTOD(THISFORM.TXTF02.VALUE+'/01')
	    =MESSAGEBOX('起始日期不得大於截止日期,請重新輸入!',0+64,'薪資系統日期範圍：'+FIRST_DATE+' ∼ '+LAST_DATE)
	    THISFORM.TXTF01.SETFOCUS
	    RETURN	  
       ENDIF	
       IF CTOD(THISFORM.TXTF02.VALUE+'/01')<CTOD(THISFORM.TXTF01.VALUE+'/01')
	    =MESSAGEBOX('截止日期不得小於起始日期,請重新輸入!',0+64,'薪資系統日期範圍：'+FIRST_DATE+' ∼ '+LAST_DATE)
	    THISFORM.TXTF02.SETFOCUS
	    RETURN	  
       ENDIF		 
*!*	       IF INDEXSEEK(DTOS(CTOD(THISFORM.TXTF01.VALUE+'/01')),.F.,'A23','A231')=.F.
*!*		   =MESSAGEBOX('起始日期在薪資系統中並不存在,請重新輸入!',0+64,'薪資系統日期範圍：'+FIRST_DATE+' ∼ '+LAST_DATE)
*!*	           THISFORM.TXTF01.SETFOCUS
*!*		   RETURN	  
*!*	       ENDIF	  
*!*	       IF INDEXSEEK(DTOS(CTOD(THISFORM.TXTF02.VALUE+'/01')),.F.,'A23','A231')=.F.
*!*		   =MESSAGEBOX('截止日期在薪資系統中並不存在,請重新輸入!',0+64,'薪資系統日期範圍：'+FIRST_DATE+' ∼ '+LAST_DATE)
*!*		   THISFORM.TXTF02.SETFOCUS
*!*		   RETURN	  
*!*	       ENDIF	          
       STAR_DATE=THISFORM.TXTF01.VALUE
       END_DATE=THISFORM.TXTF02.VALUE
       SELECT S11.* FROM S11,S05_TEMP ORDER BY S11.F02 WHERE LEFT(DTOS(S11.F01),6)>=LEFT(DTOS(CTOD(ALLTRIM(THISFORM.TXTF01.VALUE)+'/01')),6);
                      AND LEFT(DTOS(S11.F01),6)<=LEFT(DTOS(CTOD(ALLTRIM(THISFORM.TXTF02.VALUE)+'/01')),6) AND ALLTRIM(S11.F02)=ALLTRIM(S05_TEMP.F01) INTO CURSOR S11_TEMP       
       IF  _TALLY>0
	    CREATE CURSOR S12A_TEMP;
	   (F01 C(7),F02 C(4),F03 C(8),F04 C(4),F05 C(8),F06 C(1),F07 N(4,1),F08 N(7),F09 N(7),F10 N(7),;
	    F11 N(7),F12 N(7),F13 N(7),F14 N(7),F15 N(7),F16 N(7),F17 N(7),F18 N(7),F19 N(7),F20 N(7),;
	    F21 N(7),F22 N(7),F23 N(7),F24 N(7),F25 N(7),F26 N(7),F27 N(7),F28 N(7),F29 N(7),F30 N(7),F31 N(7),F32 N(7),F99 C(17))
	    INDE ON F02+F01 TAG S12A_TEMP1
	    INDE ON F04+F01 TAG S12A_TEMP2
	    SELECT S11_TEMP
	    GO TOP
	    DO WHILE !EOF()
	           SELECT S12A_TEMP 
		   APPEND BLANK 
		   REPLACE F01 WITH LEFT(DTOS(S11_TEMP.F01),4)+'/'+SUBSTR(DTOS(S11_TEMP.F01),5,2)		&&薪資日期
		   REPLACE F02 WITH S11_TEMP.F02		&&員工編號
		   REPLACE F03 WITH S11_TEMP.F03		&&員工姓名
		   REPLACE F04 WITH S11_TEMP.F04		&&部門編號
		   REPLACE F05 WITH S11_TEMP.F05		&&部門名稱
		   REPLACE F06 WITH S11_TEMP.F06		&&發薪種類( M:月薪 D:日薪 T:時薪)
		   REPLACE F07 WITH S11_TEMP.F07		&&上班天數或時數
		   REPLACE F08 WITH S11_TEMP.F08		&&遲到額外扣款小計
		   REPLACE F09 WITH S11_TEMP.F09		&&早退額外扣款小計
		   REPLACE F10 WITH S11_TEMP.F10		&&基本薪資所得總計(S05)
		   REPLACE F11 WITH S11_TEMP.F11		&&基本薪資扣款總計(S05)
		   REPLACE F12 WITH S11_TEMP.F12		&&本月基本薪資所得(不包含新增薪資所得)
		   REPLACE F13 WITH S11_TEMP.F13		&&本月新增薪資所得
		   REPLACE F14 WITH S11_TEMP.F14		&&底/日/時  薪金額
		   REPLACE F15 WITH S11_TEMP.F15		&&伙食費
		   REPLACE F16 WITH S11_TEMP.F16		&&交通費
		   REPLACE F17 WITH S11_TEMP.F17		&&全勤獎金
		   REPLACE F18 WITH S11_TEMP.F18		&&全勤扣款
		   REPLACE F19 WITH S11_TEMP.F19		&&職務津貼
		   REPLACE F20 WITH S11_TEMP.F20		&&自設項目金額
		   REPLACE F21 WITH S11_TEMP.F21		&&其他所得發放 
		   REPLACE F22 WITH S11_TEMP.F22		&&加班所得(不包括津貼金額) 
		   REPLACE F23 WITH S11_TEMP.F23		&&加班津貼補助金額
		   REPLACE F24 WITH S11_TEMP.F24		&&勞保費 
		   REPLACE F25 WITH S11_TEMP.F25		&&健保費 
		   REPLACE F26 WITH S11_TEMP.F26		&&所得稅
		   REPLACE F27 WITH S11_TEMP.F27		&&其他扣款 
		   REPLACE F28 WITH S11_TEMP.F28		&&其他所得發放扣稅金額 
		   REPLACE F29 WITH S11_TEMP.F29		&&缺勤扣款 
		   REPLACE F30 WITH S11_TEMP.F30		&&懲處扣款
		   REPLACE F31 WITH S11_TEMP.F31		&&實發薪資     
		   REPLACE F32 WITH S11_TEMP.F32		&&勞退自行提撥金額 
		   REPLACE F99 WITH S11_TEMP.F99		&&安全資料
		   SELECT S11_TEMP
		   SKIP
	     ENDDO
	     SELECT S11_TEMP
	     USE
	     SELECT S12A_TEMP
	     SET ORDER TO S12A_TEMP1
	     GO TOP		     
	     TKS12A_SEEK=CREATEOBJECT("TKS12A_SEEK")  
	     TKS12A_SEEK.SHOW		 
	     CLOSE TABLE ALL
     	     THISFORM.RELEASE 
       ELSE
	      =MESSAGEBOX('此日期範圍並無資料',0+64,'提示視窗')
	      THISFORM.TXTF01.SETFOCUS
       ENDIF    
ENDPROC       
********離開鍵***********
 PROCEDURE CMND2.CLICK     
       CLOSE TABLE ALL
       THISFORM.RELEASE 
*!*	          RETURN 
       DO S12.PRG 
 ENDPROC      
ENDDEFINE      
************************
DEFINE CLASS TKS12A_SEEK AS FORM      
*!*	  AUTOCENTER=.T.  
  CAPTION='S12A.薪資所得明細查詢'+SPACE(25)+'日期範圍：'+STAR_DATE+'∼'+END_DATE
  FONTSIZE=INT_015*9
  HEIGHT=INT_015*550
  WIDTH=INT_015*791
  CONTROLBOX=.F.
  MAXBUTTON=.F.
  MINBUTTON=.F.  
  MOVABLE=.F.
  CLOSABLE=.F.  
  BORDERSTYLE=2
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WINDOWTYPE=1
  NAME='TKS12A_SEEK'         
 ADD OBJECT LBL_REC AS LBL_REC WITH;
      TOP=INT_015*8,;
      LEFT=INT_015*630,;
      AUTOSIZE=.T.,;
      BACKCOLOR=RGB(255,255,0),;
      CAPTION='實發薪資金額：'
  ADD OBJECT KEY_LIST AS KEY_LIST WITH;
     WIDTH=INT_015*180,;
      AUTOCENTER=.T.
  ADD OBJECT GRID1 AS GRID1 WITH;
      COLUMNCOUNT=18,;            
      RECORDSOURCE='S12A_TEMP',; 
      HEIGHT=INT_015*500,;     
      WIDTH=INT_015*780,;      
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2',;
      COLUMN3.NAME='COLUMN3',; 
      COLUMN4.NAME='COLUMN4',; 
      COLUMN5.NAME='COLUMN5',;             
      COLUMN6.NAME='COLUMN6',;       
      COLUMN7.NAME='COLUMN7',;       
      COLUMN8.NAME='COLUMN8',;
      COLUMN9.NAME='COLUMN9',;   
      COLUMN10.NAME='COLUMN10',;
      COLUMN11.NAME='COLUMN11',; 
      COLUMN12.NAME='COLUMN12',;
      COLUMN13.NAME='COLUMN13',;
      COLUMN14.NAME='COLUMN14',;
      COLUMN15.NAME='COLUMN15',;
      COLUMN16.NAME='COLUMN16',;
      COLUMN17.NAME='COLUMN17',;
      COLUMN18.NAME='COLUMN18'
   ADD OBJECT CMDGROUP AS CMDGROUP WITH;
      TOP=INT_015*0,;
      LEFT=INT_015*205,;
      ENABLED=.T.,;
      VISIBLE=.T.            
  ADD OBJECT ORPGROUP AS ORPGROUP WITH;      
      TOP=INT_015*0,;
      LEFT=INT_015*405                                                  
  PROCEDURE INIT
       THISFORM.LBL_REC.CAPTION='實發薪資金額：'+ALLTRIM(TRANSFORM(S12A_TEMP.F31,'@R 999,999,999,999'))  
       THISFORM.ORPGROUP.NEW_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.EDIT_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.DEL_BOTT.VISIBLE=.F.
       THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限    
       THISFORM.GRID1.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)   
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'HEADER')   
       THISFORM.GRID1.COLUMN1.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN2.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN3.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN4.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN5.WIDTH=INT_015*55
       THISFORM.GRID1.COLUMN6.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN7.WIDTH=INT_015*70
       THISFORM.GRID1.COLUMN8.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN9.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN10.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN11.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN12.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN13.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN14.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN15.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN16.WIDTH=INT_015*50
       THISFORM.GRID1.COLUMN17.WIDTH=INT_015*80
       THISFORM.GRID1.COLUMN18.WIDTH=INT_015*120
       THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='人員編號'
       THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='人員姓名'
       THISFORM.GRID1.COLUMN3.HEADER1.CAPTION='部門編號'	   
       THISFORM.GRID1.COLUMN4.HEADER1.CAPTION='部門名稱'
       THISFORM.GRID1.COLUMN5.HEADER1.CAPTION='薪資日期'
       THISFORM.GRID1.COLUMN6.HEADER1.CAPTION='發薪總類'		
       THISFORM.GRID1.COLUMN7.HEADER1.CAPTION='上班天(時)數'	   
       THISFORM.GRID1.COLUMN8.HEADER1.CAPTION='基本薪資'
       THISFORM.GRID1.COLUMN9.HEADER1.CAPTION='本月薪資'
       THISFORM.GRID1.COLUMN10.HEADER1.CAPTION='其他所得' 
       THISFORM.GRID1.COLUMN11.HEADER1.CAPTION='加班所得'
       THISFORM.GRID1.COLUMN12.HEADER1.CAPTION='薪資扣款'  
       THISFORM.GRID1.COLUMN13.HEADER1.CAPTION='所  得  稅'  
       THISFORM.GRID1.COLUMN14.HEADER1.CAPTION='其他稅額' 	  
       THISFORM.GRID1.COLUMN15.HEADER1.CAPTION='缺勤扣款'  	 
       THISFORM.GRID1.COLUMN16.HEADER1.CAPTION='懲處扣款'  	
       THISFORM.GRID1.COLUMN17.HEADER1.CAPTION='實發薪資金額'  
       THISFORM.GRID1.COLUMN18.HEADER1.CAPTION='安全資料'  
       THISFORM.GRID1.COLUMN1.CONTROLSOURCE='S12A_TEMP.F02'
       THISFORM.GRID1.COLUMN2.CONTROLSOURCE='S12A_TEMP.F03'
       THISFORM.GRID1.COLUMN3.CONTROLSOURCE='S12A_TEMP.F04'
       THISFORM.GRID1.COLUMN4.CONTROLSOURCE='S12A_TEMP.F05'
       THISFORM.GRID1.COLUMN5.CONTROLSOURCE='S12A_TEMP.F01'
       THISFORM.GRID1.COLUMN6.CONTROLSOURCE="IIF(S12A_TEMP.F06='M','月薪',IIF(S12A_TEMP.F06='D','日薪','時薪'))"
       THISFORM.GRID1.COLUMN7.CONTROLSOURCE='S12A_TEMP.F07'
       THISFORM.GRID1.COLUMN8.CONTROLSOURCE='S12A_TEMP.F10'
       THISFORM.GRID1.COLUMN9.CONTROLSOURCE='S12A_TEMP.F12+S12A_TEMP.F13-S12A_TEMP.F21'	  				&&本月基本薪資所得(不包含新增薪資所得)+本月新增薪資所得-其他所得發放
       THISFORM.GRID1.COLUMN10.CONTROLSOURCE='S12A_TEMP.F21'
       THISFORM.GRID1.COLUMN11.CONTROLSOURCE='S12A_TEMP.F22+S12A_TEMP.F23'								&&加班所得(不包含補助金額)+加班津貼補助金額  	
       THISFORM.GRID1.COLUMN12.CONTROLSOURCE='S12A_TEMP.F24+S12A_TEMP.F25+S12A_TEMP.F27+S12A_TEMP.F32'	&&勞保費+健保費+其他扣款+勞退自行提撥金額
       THISFORM.GRID1.COLUMN13.CONTROLSOURCE='S12A_TEMP.F26'
       THISFORM.GRID1.COLUMN14.CONTROLSOURCE='S12A_TEMP.F28'
       THISFORM.GRID1.COLUMN15.CONTROLSOURCE='S12A_TEMP.F08+S12A_TEMP.F09+S12A_TEMP.F29'				&&遲到額外扣款小計+早退額外扣款小計+缺勤扣款
       THISFORM.GRID1.COLUMN16.CONTROLSOURCE='S12A_TEMP.F30'
       THISFORM.GRID1.COLUMN17.CONTROLSOURCE='S12A_TEMP.F31'
       THISFORM.GRID1.COLUMN18.CONTROLSOURCE='S12A_TEMP.F99'
       THISFORM.GRID1.READONLY=.T.       
       THISFORM.GRID1.SETFOCUS    
        IF  THISFORM.GRID1.ACTIVEROW=0 
              THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.F.      
	      THISFORM.CMDGROUP.UP_BOTT.ENABLED=.F.
	      THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.F.
	      THISFORM.CMDGROUP.END_BOTT.ENABLED=.F.    
	      THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.	        
	      THISFORM.ORPGROUP.PNT_BOTT.ENABLED=.F.   
        ELSE
              THISFORM.CMDGROUP.TOP_BOTT.ENABLED=.T.      
	      THISFORM.CMDGROUP.UP_BOTT.ENABLED=.T.
	      THISFORM.CMDGROUP.NEXT_BOTT.ENABLED=.T.
	      THISFORM.CMDGROUP.END_BOTT.ENABLED=.T.    
	      THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.	        
	      THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限       
        ENDIF          
       JK='人員編號'
  ENDPROC    
  PROC KEY_LIST.INIT 
       WITH THIS 
           .ADDITEM('依人員編號排列')
           .ADDITEM('依部門編號排列')      
       ENDWITH      
  ENDPROC 
  PROC KEY_LIST.INTERACTIVECHANGE                 
       SELE S12A_TEMP
       DO CASE
          CASE THIS.DISPLAYVALUE='依人員編號排列'    
                     THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='人員編號'
		     THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='人員姓名'
		     THISFORM.GRID1.COLUMN3.HEADER1.CAPTION='部門編號'	   
		     THISFORM.GRID1.COLUMN4.HEADER1.CAPTION='部門名稱'	   	
		     THISFORM.GRID1.COLUMN1.CONTROLSOURCE='S12A_TEMP.F02'
		     THISFORM.GRID1.COLUMN2.CONTROLSOURCE='S12A_TEMP.F03'
		     THISFORM.GRID1.COLUMN3.CONTROLSOURCE='S12A_TEMP.F04'
		     THISFORM.GRID1.COLUMN4.CONTROLSOURCE='S12A_TEMP.F05'		        
                     SET ORDER TO S12A_TEMP1
          CASE THIS.DISPLAYVALUE='依部門編號排列'         
		     THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='部門編號'	   
		     THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='部門名稱'	   	
                     THISFORM.GRID1.COLUMN3.HEADER1.CAPTION='人員編號'
		     THISFORM.GRID1.COLUMN4.HEADER1.CAPTION='人員姓名'		     
		     THISFORM.GRID1.COLUMN1.CONTROLSOURCE='S12A_TEMP.F04'
		     THISFORM.GRID1.COLUMN2.CONTROLSOURCE='S12A_TEMP.F05'	
		     THISFORM.GRID1.COLUMN3.CONTROLSOURCE='S12A_TEMP.F02'
		     THISFORM.GRID1.COLUMN4.CONTROLSOURCE='S12A_TEMP.F03'		     	        
                     SET ORDER TO S12A_TEMP2       
       ENDCASE 
       JK=THISFORM.GRID1.COLUMN1.HEADER1.CAPTION
       THISFORM.GRID1.REFRESH
       THISFORM.GRID1.SETFOCUS
  ENDPROC             
*******        
  PROC GRID1.AFTERROWCOLCHANGE
       LPARAMETERS NCOLINDEX
       THISFORM.LBL_REC.CAPTION='實發薪資金額：'+ALLTRIM(TRANSFORM(S12A_TEMP.F31,'@R 999,999,999,999'))
       FCH=THISFORM.ACTIVECONTROL.NAME
       CHK=''
       THISFORM.REFRESH
  ENDPROC       
ENDDEFINE        
***********************************************列印程序
PROCEDURE PNT_PRC  
   SELE S12A_TEMP
   ANS_RANGE=CREATEOBJECT("ANS_RANGE")  
   ANS_RANGE.SHOW       
ENDPROC 
***********************************************列印範圍選定
DEFINE CLASS ANS_RANGE AS FORM
*!*	  AUTOCENTER=.T.
  CAPTION='請輸入列印資料範圍'
  FONTSIZE=INT_015*9
  TOP=INT_015*200
  LEFT=INT_015*250    
  HEIGHT=INT_015*120
  WIDTH=INT_015*280
  FONTSIZE=INT_015*9
  MOVABLE=.F.
  MAXBUTTON=.F.
  MINBUTTON=.F.    
  CONTROLBOX=.F.
  BORDERSTYLE=2
  SHOWTIPS=.T.
  SHOWWINDOW=1
  WINDOWTYPE=1
  NAME='ANS_RANGE'
  ADD OBJECT LABEL1 AS LABEL WITH;
      AUTOSIZE=.T.,;
      HEIGHT=INT_015*25,;
      LEFT=INT_015*30,;
      TOP=INT_015*30,;
      CAPTION='起始員工編號',;
      NAME='LABEL1'
  ADD OBJECT TEXT1 AS TEXTBOX WITH;
      TOP=INT_015*24,;
      LEFT=INT_015*110,;
      HEIGHT=INT_015*25,; 
      WIDTH=INT_015*60,; 
      MAXLENGTH=4,;
      NAME='TEXT1'      
  ADD OBJECT LABEL2 AS LABEL WITH;
      AUTOSIZE=.T.,;
      HEIGHT=INT_015*25,;
      LEFT=INT_015*30,;
      TOP=INT_015*61,;
      CAPTION='截止員工編號',;
      NAME='LABEL2'
  ADD OBJECT TEXT2 AS TEXTBOX WITH;
      TOP=INT_015*55,;
      LEFT=INT_015*110,;
      HEIGHT=INT_015*25,;   
      WIDTH=INT_015*60,;     
      MAXLENGTH=4,;   
      NAME='TEXT2'    
  ADD OBJECT CMND1 AS COMMANDBUTTON WITH;
      LEFT=INT_015*190,;    
      HEIGHT=INT_015*25,; 
      WIDTH=INT_015*70,;    
      TOP=INT_015*14,;  
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<P.列         印',;
      TOOLTIPTEXT='列印所輸入的範圍鍵值!快速鍵->ALT+Y',;
      NAME='CMND1'
  ADD OBJECT CMND2 AS COMMANDBUTTON WITH;
      LEFT=INT_015*190,;    
      HEIGHT=INT_015*25,; 
      WIDTH=INT_015*70,;    
      TOP=INT_015*45,;  
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<E.轉EXCEL',;
      TOOLTIPTEXT='將資料轉至EXCEL格式!快速鍵->ALT+Y',;
      NAME='CMND2'      
  ADD OBJECT CMND3 AS COMMANDBUTTON WITH;
      LEFT=INT_015*190,;
      TOP=INT_015*76,;
      HEIGHT=INT_015*25,;
      WIDTH=INT_015*70,; 
      MOUSEPOINTER=99,;
      MOUSEICON='BMPS\harrow.cur',;        
      CAPTION='\<C.取        消',;
      TOOLTIPTEXT='取消作業!快速鍵->ALT+C',;
      NAME='CMND3'
  PROCEDURE INIT 
      THISFORM.SETALL('FONTSIZE',INT_015*10,'TEXTBOX')
      THISFORM.SETALL('FONTSIZE',INT_015*10,'LABEL')      
      THISFORM.SETALL('FONTSIZE',INT_015*10,'COMMANDBUTTON')    
      DO CASE  
             CASE KEY()='F02+F01'     
                        THISFORM.LABEL1.CAPTION='起始員工編號' 
                        THISFORM.LABEL2.CAPTION='截止員工編號' 
                        THISFORM.TEXT1.VALUE=S12A_TEMP.F02
                        THISFORM.TEXT2.VALUE=S12A_TEMP.F02
             CASE KEY()='F04+F01'    
                        THISFORM.LABEL1.CAPTION='起始部門編號' 
                        THISFORM.LABEL2.CAPTION='截止部門編號'        
                        THISFORM.TEXT1.VALUE=S12A_TEMP.F04
                        THISFORM.TEXT2.VALUE=S12A_TEMP.F04                              
      ENDCASE     
  ENDPROC    
  PROCEDURE CMND1.CLICK 
      IF  EMPTY(THISFORM.TEXT1.VALUE)
           =MESSAGEBOX('起始編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT1.SETFOCUS
  	   RETURN 
      ENDIF
      IF  EMPTY(THISFORM.TEXT2.VALUE)
           =MESSAGEBOX('截止編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT2.SETFOCUS
  	   RETURN 
      ENDIF      
      DO CASE  
             CASE KEY()='F02+F01'     
                        SELECT  * FROM S12A_TEMP WHERE F02>=THISFORM.TEXT1.VALUE AND F02<=THISFORM.TEXT2.VALUE  ORDER BY F02,F01 NOWAIT
		        IF  _TALLY >0
		             REPORT FORM ALLTRIM(INT_116)+'S12A_1'  TO PRINTER PROMPT  PREVIEW  
		        ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                          
             CASE KEY()='F04+F01'    
                        SELECT  * FROM S12A_TEMP WHERE F04>=THISFORM.TEXT1.VALUE AND F04<=THISFORM.TEXT2.VALUE  ORDER BY F04,F01 NOWAIT                     
		        IF  _TALLY >0
		             REPORT FORM ALLTRIM(INT_116)+'S12A_2' TO PRINTER PROMPT  PREVIEW
		        ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                               
      ENDCASE         
           THISFORM.CMND3.CLICK
  ENDPROC           
  ****
    PROCEDURE CMND2.CLICK 
      IF  EMPTY(THISFORM.TEXT1.VALUE)
           =MESSAGEBOX('起始編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT1.SETFOCUS
  	   RETURN 
      ENDIF
      IF  EMPTY(THISFORM.TEXT2.VALUE)
           =MESSAGEBOX('截止編號不得為空白!',0+64,'提示訊息視窗')
           THISFORM.TEXT2.SETFOCUS
  	   RETURN 
      ENDIF      
      FILENAME='S12A 薪資所得明細'+LEFT(STAR_DATE,4)+RIGHT(STAR_DATE,2)+'~'+LEFT(END_DATE,4)+RIGHT(END_DATE,2)
      DO CASE  
             CASE KEY()='F02+F01'     
                        SELECT F02 員工編號,F03 員工姓名,F04 部門編號,F05 部門名稱,F01 薪資日期,F07 上班天數,F10 基本薪資,F12 本月薪資,;
                                       F13 其他所得,F22+F23 加班所得,F24+F25+F27+F28+F32 薪資扣款,F08+F09+F29 缺勤扣款,F30 懲處扣款,F26 所的稅,;
                                       F31 實發薪資 FROM S12A_TEMP WHERE F02>=THISFORM.TEXT1.VALUE AND F02<=THISFORM.TEXT2.VALUE ORDER BY F02 NOWAIT
		        IF  _TALLY >0
	                     GCDELIMFILE=PUTFILE('儲存路徑',FILENAME,'XLS')
			     IF !EMPTY(GCDELIMFILE) 
			          ON ERROR DO FILE_IMPACT
			          COPY TO (GCDELIMFILE) TYPE XL5     
			          =MESSAGEBOX('儲存成功',0+64,'提示訊息視窗') 
			     ELSE
		                 THISFORM.TEXT1.SETFOCUS
		                 RETURN			    
			     ENDIF  	
			ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                          
             CASE KEY()='F04+F01'    
                        SELECT F04 部門編號,F05 部門名稱,F02 員工編號,F03 員工姓名,F01 薪資日期,F07 上班天數,F10 基本薪資,F12 本月薪資,;
                                       F13 其他所得,F22+F23 加班所得,F24+F25+F27+F28+F32 薪資扣款,F08+F09+F29 缺勤扣款,F30 懲處扣款,F26 所的稅,;
                                       F31 實發薪資 FROM S12A_TEMP WHERE F04>=THISFORM.TEXT1.VALUE AND F04<=THISFORM.TEXT2.VALUE ORDER BY F02 NOWAIT
		        IF  _TALLY >0
	                     GCDELIMFILE=PUTFILE('儲存路徑',FILENAME,'XLS')
			     IF !EMPTY(GCDELIMFILE) 
			          ON ERROR DO FILE_IMPACT
			          COPY TO (GCDELIMFILE) TYPE XL5     
			          =MESSAGEBOX('儲存成功',0+64,'提示訊息視窗') 
			     ELSE
		                 THISFORM.TEXT1.SETFOCUS
		                 RETURN			     
			     ENDIF  	
			ELSE
		             =MESSAGEBOX('查無此範圍資料',0+48,'警示')
		             THISFORM.TEXT1.SETFOCUS
		             RETURN     
		        ENDIF                               
      ENDCASE         
           THISFORM.CMND3.CLICK
  ENDPROC        
  ****
  PROCEDURE CMND3.CLICK
      THISFORM.RELEASE
  ENDPROC      
  PROC CMND1.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND1.TOP=INT_015*15
             THISFORM.CMND1.LEFT=INT_015*191
  ENDPROC     
  PROC CMND1.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND1.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND1.TOP=INT_015*14
             THISFORM.CMND1.LEFT=INT_015*190                  
  ENDPROC     
  PROC CMND2.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND2.TOP=INT_015*46
             THISFORM.CMND2.LEFT=INT_015*191
  ENDPROC     
  PROC CMND2.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND2.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND2.TOP=INT_015*45
             THISFORM.CMND2.LEFT=INT_015*190                  
  ENDPROC     
  PROC CMND3.MOUSEENTER      
       LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND3.FORECOLOR=RGB(255,0,0)
             THISFORM.CMND3.TOP=INT_015*77
             THISFORM.CMND3.LEFT=INT_015*191
  ENDPROC     
  PROC CMND3.MOUSELEAVE      
        LPARAMETERS nButton, nShift, nXCoord, nYCoord 
             THISFORM.CMND3.FORECOLOR=RGB(0,0,0)  
             THISFORM.CMND3.TOP=INT_015*76
             THISFORM.CMND3.LEFT=INT_015*190                  
  ENDPROC      
ENDDEFINE    