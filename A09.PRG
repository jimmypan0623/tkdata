close all
clear 
FLG='0'
FCH=''
AREA1='A14'
CHK=''
IF !USED('A01')
   SELE 0
   USE A01 INDE A01
ELSE 
   SELE A01
ENDIF
SET ORDER TO A01      
IF !USED('A02')
   SELE 0
   USE A02 INDE A02
ELSE 
   SELE A02
ENDIF
SET ORDER TO A021      
SEEK sys_oper+'A09'   
IF !USED('A14')
   SELE 0
   USE A14 INDE A14
ELSE 
   SELE A14
ENDIF
SET ORDER TO 1
SET FILTER TO LEN(ALLTRIM(F01))=4
GO TOP

A09form=createobject("tkA09")
A09form.show  
define class tkA09 as form
  caption='A09.部門設定'
*!*	  autocenter=.t.
  controlbox=.F.
  BORDERSTYLE=1  
  MAXBUTTON=.F.
  MINBUTTON=.F.
  MOVABLE=.F.
  CLOSABLE=.F.
  controlcount=57
  FONTSIZE=INT_015*9
  HEIGHT=INT_015*550
  showtips=.t.
  showwindow=1
  WIDTH=INT_015*789
  windowtype=1
  NAME='TKA09'
  add object shape1 as shape1 with;
      autocenter=.t.
  add object shape2 as shape2 with;
      autocenter=.t.
  add object shape3 as shape3 with;
      autocenter=.t.
  ADD OBJECT LBL_REC AS LBL_REC WITH;
      AUTOCENTER =.T.
  ADD OBJECT REC_COUNT AS REC_COUNT  with;
      caption=str(reccount())      

  ADD OBJECT LBLF01 AS LABEL WITH;
      LEFT=INT_015*244,;
      TOP=INT_015*27,;
      WIDTH=INT_015*50,;
      CAPTION='部門編號'
  ADD OBJECT LBLF02 AS LABEL WITH;      
      LEFT=INT_015*244,;
      TOP=INT_015*52,;
      WIDTH=INT_015*50,;
      CAPTION='部門名稱'   
  ADD OBJECT LBLF06 AS LABEL WITH;
      LEFT=INT_015*244,;
      TOP=INT_015*102,;
      WIDTH=INT_015*50,;
      CAPTION='備註說明'
  ADD OBJECT LBLF07 AS LABEL WITH;
      LEFT=INT_015*244,;
      TOP=INT_015*127,;
      WIDTH=INT_015*50,; 
      CAPTION='安全資料'   
  ADD OBJECT LBLF071 AS LABEL WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*127,;
      AUTOSIZE=.T.
  ADD OBJECT TXTF01 AS TEXTBOX WITH;      
      LEFT=INT_015*295,;
      TOP=INT_015*23,;
      WIDTH=INT_015*49,;
      MAXLENGTH=5,;
      INPUTMASK='XXXX',;
      NAME='TXTF01'  
  ADD OBJECT TXTF02 AS TEXTBOX WITH;
      AUTOSIZE=.T.,;
      LEFT=INT_015*295,;
      TOP=INT_015*48,;
      WIDTH=INT_015*68,;
      MAXLENGTH=8,;
      NAME='TXTF02'                 
  ADD OBJECT CHKF04 AS CHECKBOX WITH;
      LEFT=INT_015*230,;
      TOP=INT_015*77,;
      ALIGNMENT=1,;
      AUTOSIZE=.T.,;
      CAPTION='列入庫存'                    
  ADD OBJECT CHKF05 AS CHECKBOX WITH;
      LEFT=INT_015*325,;
      TOP=INT_015*77,;
      ALIGNMENT=1,;
      AUTOSIZE=.T.,;
      CAPTION='是否生產'                   
  ADD OBJECT CHKF13 AS CHECKBOX WITH;
      LEFT=INT_015*420,;
      TOP=INT_015*77,;
      ALIGNMENT=1,;
      AUTOSIZE=.T.,;
      CAPTION='列入成本'                   
  ADD OBJECT TXTF06 AS TEXTBOX WITH;
      LEFT=INT_015*295,;
      TOP=INT_015*98,;
      WIDTH=INT_015*177,;            
      MAXLENGTH=24,;
      NAME='TXTF06'                           
  ADD OBJECT KEY_LIST AS KEY_LIST WITH;
      AUTOCENTER=.T.
  ADD OBJECT GRID1 AS GRID1 WITH;
      columncount=2,;            
      RECORDSOURCE='A14',;      
      COLUMN1.NAME='COLUMN1',;
      COLUMN2.NAME='COLUMN2'
  ADD OBJECT CMDGROUP AS CMDGROUP WITH;
      AUTOSIZE=.T.              
  ADD OBJECT ORPGROUP AS ORPGROUP WITH;    
      AUTOSIZE=.T.
  ADD OBJECT SHRGROUP AS SHRGROUP WITH;
      TOP=INT_015*455,;
      LEFT=INT_015*558          
  PROC INIT
       THISFORM.ORPGROUP.NEW_BOTT.ENABLED=IIF(A02.F04='*',.T.,.F.)   &&判斷有無新增的權限
       THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=IIF(A02.F05='*',.T.,.F.)  &&判斷有無修改的權限
       THISFORM.ORPGROUP.DEL_BOTT.ENABLED=IIF(A02.F06='*',.T.,.F.)   &&判斷有無刪除的權限
       THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限
       THISFORM.SHRGROUP.VISIBLE=.F.
       THISFORM.SHRGROUP.ENABLED=.F.
       thisform.setall('height',INT_015*25,'textbox')
       thisform.setall('height',INT_015*17,'label')
       thisform.setall('FONTSIZE',INT_015*9,'textbox')
       thisform.setall('FONTSIZE',INT_015*9,'label')       
       thisform.setall('FONTSIZE',INT_015*9,'CHECKBOX')              
       THISFORM.SETALL('READONLY',.T.,'TEXTBOX')
       THISFORM.CHKF04.READONLY=.T.
       THISFORM.CHKF05.READONLY=.T.
       THISFORM.CHKF13.READONLY=.T.
       THISFORM.GRID1.COLUMN1.WIDTH=INT_015*49
       THISFORM.GRID1.COLUMN2.WIDTH=INT_015*68
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'COLUMN')
       THISFORM.GRID1.SETALL('FONTSIZE',INT_015*9,'HEADER')       
       THISFORM.GRID1.BACKCOLOR=IIF(SEEK(sys_oper,'A01'),IIF(A01.F11=0,10009146,A01.F11),10009146)
       THISFORM.GRID1.COLUMN1.HEADER1.CAPTION='部門編號'
	   THISFORM.GRID1.COLUMN2.HEADER1.CAPTION='部門名稱'
	   THISFORM.GRID1.COLUMN1.CONTROLSOURCE='A14.F01'
	   THISFORM.GRID1.COLUMN2.CONTROLSOURCE='A14.F02'
       THISFORM.GRID1.READONLY=.T.       
       THISFORM.GRID1.SETFOCUS
       IF THISFORM.GRID1.ACTIVEROW=0
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.F.
       ELSE
          THISFORM.CMDGROUP.SEEK_BOTT.ENABLED=.T.      
       ENDIF       
       JK='部門編號'
  ENDPROC     

  PROC KEY_LIST.INIT 
       with this 
           .additem('依部門編號排列')
       endwith      
  ENDPROC 
  PROC GRID1.AFTERROWCOLCHANGE
       LPARAMETERS nColIndex
       SELECT A14
       THISFORM.TXTF01.VALUE=F01
       THISFORM.TXTF02.VALUE=F02
       THISFORM.CHKF04.VALUE=IIF(F04='*',1,0)
       THISFORM.CHKF05.VALUE=IIF(F05='*',1,0)   
       THISFORM.CHKF13.VALUE=IIF(F13='*',1,0)    
       THISFORM.TXTF06.VALUE=F06
       THISFORM.LBLF071.CAPTION=F07       
       FCH=THISFORM.ACTIVECONTROL.NAME
       CHK=''
       thisform.refresh
  ENDPROC       
  PROCEDURE ORPGROUP.NEW_BOTT.CLICK    
       THISFORM.SETALL('READONLY',.F.,'TEXTBOX') 
       THISFORM.CHKF04.READONLY=.F.   
       THISFORM.CHKF05.READONLY=.F.          
       THISFORM.CHKF13.READONLY=.F.                 
       THISFORM.TXTF01.VALUE=''
       THISFORM.TXTF02.VALUE=''
       THISFORM.CHKF04.VALUE=0
       THISFORM.CHKF05.VALUE=0       
       THISFORM.CHKF13.VALUE=0
       THISFORM.TXTF06.VALUE=''
       THISFORM.LBLF071.CAPTION=''
       THISFORM.TXTF01.SETFOCUS()
       THISFORM.GRID1.ENABLED=.F.
       FLG='1'
  ENDPROC  
  PROCEDURE ORPGROUP.EDIT_BOTT.CLICK
     THISFORM.TXTF01.READONLY=.T.
     THISFORM.GRID1.ENABLED=.F.
     THISFORM.TXTF02.SETFOCUS
     THISFORM.CHKF04.READONLY=.F.
     THISFORM.CHKF05.READONLY=.F.
     THISFORM.CHKF13.READONLY=.F.
     RLOCK()
     IF !RLOCK()
        DO file_impact
        return
     endif
  endproc 
  PROCEDURE ORPGROUP.DEL_BOTT.CLICK
     if messagebox('是否確定要刪除此筆資料',4+32+256,'請確認') = 6	      
        IF !USED('B11')
           SELE 0
           USE B11
        ELSE
           SELE B11
        ENDIF
        SET ORDER TO B111
        SEEK A14.F01
        IF FOUND()
           =MESSAGEBOX('此部門尚有庫存數量!不得刪除',0+48,'提示訊息視窗')
           USE
           RETURN
        ELSE
          USE   
        ENDIF
                      
        SELE A14
        LOCK()
        IF RLOCK()        
           DELE
           unlock
           SKIP -1
           IF BOF()
              GO TOP
           ENDIF   
           THISFORM.GRID1.SETFOCUS
           IF THISFORM.GRID1.ACTIVEROW=0
              THISFORM.SETALL('VALUE','','TEXTBOX')
              THISFORM.CMDGROUP.ENABLED=.F.
              THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=.F.
              THISFORM.ORPGROUP.DEL_BOTT.ENABLED=.F.
              THISFORM.ORPGROUP.PNT_BOTT.ENABLED=.F.
           ELSE
              THISFORM.CMDGROUP.ENABLED=.T.
              THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=IIF(A02.F05='*',.T.,.F.)  &&判斷有無修改的權限
              THISFORM.ORPGROUP.DEL_BOTT.ENABLED=IIF(A02.F06='*',.T.,.F.)   &&判斷有無刪除的權限
              THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限
           ENDIF   
        ELSE
          DO file_impact    
        ENDIF     
     ENDIF
  ENDPROC
  PROCEDURE SHRGROUP.SHURE1_BOTT.CLICK
    IF EMPTY(THISFORM.TXTF01.VALUE)
       =MESSAGEBOX('部門編號不得空白!',0+64,'提示視窗')
       THISFORM.TXTF01.SETFOCUS
       RETURN
    ENDIF   
    IF SEEK(THISFORM.TXTF01.VALUE,'A14')=.T.
       =MESSAGEBOX('此部門編號已經建立!',0+64,'請注意')
       THISFORM.TXTF01.VALUE=''
       THISFORM.TXTF01.SETFOCUS  
       RETURN
    ENDIF
    APPEND BLANK
    SELECT A14
    LOCK()
    IF RLOCK()
       REPLACE F01 WITH THISFORM.TXTF01.VALUE
       REPLACE F02 WITH THISFORM.TXTF02.VALUE
       REPLACE F04 WITH IIF(THISFORM.CHKF04.VALUE=1,'*','')
       REPLACE F05 WITH IIF(THISFORM.CHKF05.VALUE=1,'*','')
       REPLACE F13 WITH IIF(THISFORM.CHKF13.VALUE=1,'*','')       
       REPLACE F06 WITH THISFORM.TXTF06.VALUE
       REPLACE F07 WITH IIF(INT_012=1,PADL(ALLTRIM(STR(YEAR(DATE())-1911)),3,' ')+RIGHT(DTOC(DATE()),6),dtoc(DATE())+' ')+IIF(SEEK(sys_oper,'A01'),A01.F03,'')
    ENDIF
    UNLOCK
    THISFORM.REC_COUNT.CAPTION=STR(RECCOUNT())
    THISFORM.GRID1.ENABLED=.T.
    THISFORM.REFRESH
    THISFORM.GRID1.SETFOCUS    
    THISFORM.ORPGROUP.NEW_BOTT.CLICK            
  ENDPROC
  PROCEDURE SHRGROUP.SHURE2_BOTT.CLICK
    SELE A14
    IF RLOCK()
       REPLACE F02 WITH THISFORM.TXTF02.VALUE       
       REPLACE F04 WITH IIF(THISFORM.CHKF04.VALUE=1,'*','')
       REPLACE F05 WITH IIF(THISFORM.CHKF05.VALUE=1,'*','')
       REPLACE F13 WITH IIF(THISFORM.CHKF13.VALUE=1,'*','')       
       REPLACE F06 WITH THISFORM.TXTF06.VALUE
       REPLACE F07 WITH IIF(INT_012=1,PADL(ALLTRIM(STR(YEAR(DATE())-1911)),3,' ')+RIGHT(DTOC(DATE()),6),dtoc(DATE())+' ')+IIF(SEEK(sys_oper,'A01'),A01.F03,'')       
    ELSE
      DO file_impact
      return   
    ENDIF   
    THISFORM.SHRGROUP.ABANDON_BOTT.CLICK    
  ENDPROC   
  procedure SHRGROUP.ABANDON_BOTT.CLICK
      THISFORM.SHRGROUP.ENABLED=.F.
      THISFORM.SHRGROUP.VISIBLE=.F.      
      THISFORM.ORPGROUP.ENABLED=.T.
      THISFORM.ORPGROUP.SETALL('FORECOLOR',RGB(0,0,0),'COMMANDBUTTON')
      THISFORM.ORPGROUP.NEW_BOTT.ENABLED=IIF(A02.F04='*',.T.,.F.)   &&判斷有無新增的權限      
      THISFORM.ORPGROUP.EDIT_BOTT.ENABLED=IIF(A02.F05='*',.T.,.F.)  &&判斷有無修改的權限
      THISFORM.ORPGROUP.DEL_BOTT.ENABLED=IIF(A02.F06='*',.T.,.F.)   &&判斷有無刪除的權限
      THISFORM.ORPGROUP.PNT_BOTT.ENABLED=IIF(A02.F07='*',.T.,.F.)   &&判斷有無列印的權限      
      THISFORM.CMDGROUP.ENABLED=.T.
      THISFORM.CMDGROUP.SETALL('ENABLED',.T.,'COMMANDBUTTON')
      THISFORM.CMDGROUP.SETALL('FORECOLOR',RGB(0,0,0),'COMMANDBUTTON')
      THISFORM.KEY_LIST.ENABLED=.T.      
      THISFORM.SETALL('READONLY',.T.,'TEXTBOX')   
      THISFORM.CHKF04.READONLY=.T.
      THISFORM.CHKF05.READONLY=.T.
      THISFORM.CHKF13.READONLY=.T.
      UNLOCK ALL
      SELE A01
      THISFORM.GRID1.ENABLED=.T.
      THISFORM.GRID1.SETFOCUS       
  ENDPROC
  PROCEDURE SHRGROUP.FINISH_BOTT.CLICK   
       THISFORM.SHRGROUP.ABANDON_BOTT.CLICK
  ENDPROC     
enddefine    

